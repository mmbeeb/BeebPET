
	;**********************************
	;*COMMODORE PET BASIC             *
	;*                                *
	;*VERSION 2                       *
	;*COPYRIGHT 1978 BY               *
	;*COMMODORE INTERNATIONAL LIMITED *
	;*ROMS PRODUCED: 901465-01        *
	;*               901465-02        *
	;*               901447-24        *
	;*RECONSTRUCTED: M.HOFFMANN-VETTER*
	;**********************************


.STMDSP
	EQUW END-1
	EQUW FOR-1
	EQUW NEXT-1
	EQUW DATA-1
	EQUW INPUTN-1
	EQUW INPUT-1
	EQUW DIM-1
	EQUW BREAD-1
	EQUW LET-1
	EQUW GOTO-1
	EQUW RUN-1
	EQUW IF-1
	EQUW RESTOR-1
	EQUW GOSUB-1
	EQUW RETURN-1
	EQUW REM-1
	EQUW STOP-1
	EQUW ONGOTO-1
	EQUW FNWAIT-1
	EQUW CLOAD-1
	EQUW CSAVE-1
	EQUW CVERF-1
	EQUW DEF-1
	EQUW POKE-1
	EQUW PRINTN-1
	EQUW PRINT-1
	EQUW CONT-1
	EQUW LIST-1
	EQUW CLEAR-1
	EQUW CMD-1
	EQUW CSYS-1
	EQUW COPEN-1
	EQUW CCLOS-1
	EQUW GET-1
	EQUW SCRATH-1
	
.FUNDSP
	EQUW SGN
	EQUW INT
	EQUW ABS
.USRLOC
	EQUW USRPOK
	EQUW FRE
	EQUW POS
	EQUW SQR
	EQUW RND
	EQUW LOG
	EQUW EXP
	EQUW COS
	EQUW SIN
	EQUW TAN
	EQUW ATN
	EQUW PEEK
	EQUW LEN
	EQUW STRD
	EQUW VAL
	EQUW ASC
	EQUW CHRD
	EQUW LEFTD
	EQUW RIGHTD
	EQUW MIDD

.OPTAB
	EQUB 121
	EQUW FADDT-1
	EQUB 121
	EQUW FSUBT-1
	EQUB 123
	EQUW FMULTT-1
	EQUB 123
	EQUW FDIVT-1
	EQUB 127
	EQUW FPWRT-1
	EQUB 80
	EQUW ANDOP-1
	EQUB 70
	EQUW OROP-1
.NEGTAB
	EQUB 125
	EQUW NEGOP-1
.NOTTAB
	EQUB 90
	EQUW NOTOP-1
.PTDORL
	EQUB 100
	EQUW DOREL-1
	
	;RESERVED WORD LIST
.RESLST
	XSTR "END"
	ENDTK = 128
	XSTR "FOR"
	FORTK = 129
	XSTR "NEXT"
	XSTR "DATA"
	DATATK = 131
	XSTR "INPUT#"
	XSTR "INPUT"
	XSTR "DIM"
	XSTR "READ"
	XSTR "LET"
	XSTR "GOTO"
	GOTOTK = 137
	XSTR "RUN"
	XSTR "IF"
	XSTR "RESTORE"
	XSTR "GOSUB"
	GOSUTK = 141
	XSTR "RETURN"
	XSTR "REM"
	REMTK = 143
	XSTR "STOP"
	XSTR "ON"
	XSTR "WAIT"
	XSTR "LOAD"
	XSTR "SAVE"
	XSTR "VERIFY"
	XSTR "DEF"
	XSTR "POKE"
	XSTR "PRINT#"
	XSTR "PRINT"
	PRINTK = 153
	XSTR "CONT"
	XSTR "LIST"
	XSTR "CLR"
	XSTR "CMD"
	XSTR "SYS"
	XSTR "OPEN"
	XSTR "CLOSE"
	XSTR "GET"
	XSTR "NEW"
	SCRATK = 162
	XSTR "TAB("
	TABTK = 163
	XSTR "TO"
	TOTK = 164
	XSTR "FN"
	FNTK = 165
	XSTR "SPC("
	SPCTK = 166
	XSTR "THEN"
	THENTK = 167
	XSTR "NOT"
	NOTTK = 168
	XSTR "STEP"
	STEPTK = 169
	XSTR "+"
	PLUSTK = 170
	XSTR "-"
	MINUTK = 171
	XSTR "*"
	XSTR "/"
	XSTR "^"
	XSTR "AND"
	XSTR "OR"
	XSTR ">"
	GREATK = 177
	XSTR "="
	EQULTK = 178
	XSTR "<"
	LESSTK = 179
	XSTR "SGN"
	ONEFUN = 180
	XSTR "INT"
	XSTR "ABS"
	XSTR "USR"
	XSTR "FRE"
	XSTR "POS"
	XSTR "SQR"
	XSTR "RND"
	XSTR "LOG"
	XSTR "EXP"
	XSTR "COS"
	XSTR "SIN"
	XSTR "TAN"
	XSTR "ATN"
	XSTR "PEEK"
	XSTR "LEN"
	XSTR "STR$"
	XSTR "VAL"
	XSTR "ASC"
	XSTR "CHR$"
	LASNUM = 199
	XSTR "LEFT$"
	XSTR "RIGHT$"
	XSTR "MID$"
	XSTR "GO"
	GOTK = 203
	EQUB 0
	
	;BASIC ERROR MESSAGES
.ERRTAB
	ERRNF = *-ERRTAB
	XSTR "NEXT WITHOUT FOR"
	ERRSN = *-ERRTAB
	XSTR "SYNTAX"
	ERRRG = *-ERRTAB
	XSTR "RETURN WITHOUT GOSUB"
	ERROD = *-ERRTAB
	XSTR "OUT OF DATA"
	ERRFC = *-ERRTAB
	XSTR "ILLEGAL QUANTITY"
	ERROV = *-ERRTAB
	XSTR "OVERFLOW"
	ERROM = *-ERRTAB
	XSTR "OUT OF MEMORY"
	ERRUS = *-ERRTAB
	XSTR "UNDEF'D STATEMENT"
	ERRBS = *-ERRTAB
	XSTR "BAD SUBSCRIPT"
	ERRDD = *-ERRTAB
	XSTR "REDIM'D ARRAY"
	ERRDVO = *-ERRTAB
	XSTR "DIVISION BY ZERO"
	ERRID = *-ERRTAB
	XSTR "ILLEGAL DIRECT"
	ERRTM = *-ERRTAB
	XSTR "TYPE MISMATCH"
	ERRLS = *-ERRTAB
	XSTR "STRING TOO LONG"
	ERRBD = *-ERRTAB
	XSTR "FILE DATA"
	ERRST = *-ERRTAB
	XSTR "FORMULA TOO COMPLEX"
	ERRCN = *-ERRTAB
	XSTR "CAN'T CONTINUE"
	ERRUF = *-ERRTAB
	XSTR "UNDEF'D FUNCTION"

.ERR
	EQUB " ERROR", 0
	
.INTXT
	EQUB " IN ", 0
	
.REDDY
	EQUB 13, 10, "READY.", 13, 10, 0
	
.BRKTXT
	EQUB 13, 10, "BREAK", 0

	;PET BASIC
	;START OF INTERPRETER CODE
	
.FNDFOR
	TSX
	INX
	INX
	INX
	INX
	
.FFLOOP
	LDA 257,X
	CMP #FORTK
	BNE FFRTS
	
	LDA FORPNT+1
	BNE CMPFOR
	
	LDA 258,X
	STA FORPNT
	LDA 259,X
	STA FORPNT+1
	
.CMPFOR
	CMP 259,X
	BNE ADDFRS
	
	LDA FORPNT
	CMP 258,X
	BEQ FFRTS
	
.ADDFRS
	TXA
	CLC
	ADC #FORSIZ
	TAX
	BNE FFLOOP
	
.FFRTS
	RTS

.BLTU
	JSR REASON
	STA STREND
	STY STREND+1
	
.BLTUC
	SEC
	LDA HIGHTR
	SBC LOWTR
	STA INDEX
	TAY
	LDA HIGHTR+1
	SBC LOWTR+1
	TAX
	INX
	TYA
	BEQ DECBLT
	
	LDA HIGHTR
	SEC
	SBC INDEX
	STA HIGHTR
	BCS BLT1
	
	DEC HIGHTR+1
	SEC
	
.BLT1
	LDA HIGHDS
	SBC INDEX
	STA HIGHDS
	BCS MOREN1
	
	DEC HIGHDS+1
	BCC MOREN1
	
.BLTLP
	LDA (HIGHTR),Y
	STA (HIGHDS),Y
	
.MOREN1
	DEY
	BNE BLTLP
	
	LDA (HIGHTR),Y
	STA (HIGHDS),Y
	
.DECBLT
	DEC HIGHTR+1
	DEC HIGHDS+1
	DEX
	BNE MOREN1
	
	RTS
	
.GETSTK
	ASL A
	ADC #NUMLEV+NUMLEV+16
	BCS OMERR
	
	STA INDEX
	TSX
	CPX INDEX
	BCC OMERR
	
	RTS
	
.REASON
	CPY FRETOP+1
	BCC REARTS
	BNE TRYMOR
	
	CMP FRETOP
	BCC REARTS
	
.TRYMOR
	PHA
	LDX #8+ADDPRC
	TYA
	
.REASAV
	PHA
	LDA HIGHDS-1,X
	DEX
	BPL REASAV
	
	JSR GARBA2
	LDX #248-ADDPRC
	
.REASTO
	PLA
	STA HIGHDS+8+ADDPRC,X
	INX
	BMI REASTO
	
	PLA
	TAY
	PLA
	CPY FRETOP+1
	BCC REARTS
	BNE OMERR
	
	CMP FRETOP
	BCS OMERR
	
.REARTS
	RTS
	
.OMERR
	LDX #ERROM

.ERROR
	LSR CNTWFL
	LDA CHANNL
	BEQ ERRCRD
	
	JSR CLSCHN
	LDA #0
	STA CHANNL
	
.ERRCRD
	JSR CRDO
	JSR OUTQST
	
.GETERR
	LDA ERRTAB,X
	PHA
	AND #127
	JSR OUTDO
	INX
	PLA
	BPL GETERR
	
.TYPERR
	JSR STKINI
	LDA #<ERR
	LDY #>ERR
	
.ERRFIN
	JSR STROUT
	LDY CURLIN+1
	INY
	BEQ READY
	
	JSR INPRT

.READY
	LSR CNTWFL
	LDA #<REDDY
	LDY #>REDDY
	JSR STROUT	;PRINT "READY."

.MAIN	; MAIN LOOP
	JSR INLIN
	
	STX TXTPTR
	STY TXTPTR+1
	JSR CHRGET
	TAX
	BEQ MAIN
	
	LDX #255
	STX CURLIN+1
	BCC MAIN1
	
	JSR CRUNCH
	JMP GONE
	
.MAIN1
	JSR LINGET
	JSR CRUNCH
	STY COUNT
	JSR FNDLIN
	BCC NODEL
	
	LDY #1
	LDA (LOWTR),Y
	STA INDEX1+1
	LDA VARTAB
	STA INDEX1
	LDA LOWTR+1
	STA INDEX2+1
	LDA LOWTR
	DEY
	SBC (LOWTR),Y
	CLC
	ADC VARTAB
	STA VARTAB
	STA INDEX2
	LDA VARTAB+1
	ADC #255
	STA VARTAB+1
	SBC LOWTR+1
	TAX
	SEC
	LDA LOWTR
	SBC VARTAB
	TAY
	BCS QDECT1
	
	INX
	DEC INDEX2+1
	
.QDECT1
	CLC
	ADC INDEX1
	BCC MLOOP
	
	DEC INDEX1+1
	CLC
	
.MLOOP
	LDA (INDEX1),Y
	STA (INDEX2),Y
	INY
	BNE MLOOP
	
	INC INDEX1+1
	INC INDEX2+1
	DEX
	BNE MLOOP
	
.NODEL
	JSR RUNC
	JSR LNKPRG
	LDA BUF
	BEQ MAIN
	
	CLC
	LDA VARTAB
	STA HIGHTR
	ADC COUNT
	STA HIGHDS
	LDY VARTAB+1
	STY HIGHTR+1
	BCC NODELC
	
	INY
	
.NODELC
	STY HIGHDS+1
	JSR BLTU
	LDA LINNUM
	LDY LINNUM+1
	STA BUF-2
	STY BUF-1
	LDA STREND
	LDY STREND+1
	STA VARTAB
	STY VARTAB+1
	LDY COUNT
	DEY
	
.STOLOP
	LDA BUF-4,Y
	STA (LOWTR),Y
	DEY
	BPL STOLOP
	
.FINI
	JSR RUNC
	JSR LNKPRG
	JMP MAIN
	
.LNKPRG
	LDA TXTTAB
	LDY TXTTAB+1
	STA INDEX
	STY INDEX+1
	CLC
	
.CHEAD
	LDY #1
	LDA (INDEX),Y
	BEQ LNKRTS
	LDY #4
	
.CZLOOP	INY
	LDA (INDEX),Y
	BNE CZLOOP
	
	INY
	TYA
	ADC INDEX
	TAX
	LDY #0
	STA (INDEX),Y
	LDA INDEX+1
	ADC #0
	INY
	STA (INDEX),Y
	STX INDEX
	STA INDEX+1
	BCC CHEAD
	
.LNKRTS
	RTS
	
.INLIN		; ENTER LINE
{
	LDX #0
	
.INLINC
	JSR INCHR
	
	CMP #13
	BEQ FININ1
	
	STA BUF,X
	INX
	BNE INLINC
	
.FININ1
	JMP FININL
	
.INCHR
	JSR CINCH
	
	LDY CHANNL
	BNE INCRTS
	
	CMP #$0F
	BNE INCRTS
	
	PHA
	LDA CNTWFL
	EOR #$FF
	STA CNTWFL
	PLA
	
.INCRTS
	RTS
}

	BUFOFS	= BUF	;512		;?????????????????????????????
	
.CRUNCH
	LDX TXTPTR
	LDY #4
	STY DORES
	
.KLOOP
	LDA BUFOFS,X
	BPL CMPSPC
	
	CMP #XPI
	BEQ STUFFH
	
	INX
	BNE KLOOP
	
.CMPSPC
	CMP #' '
	BEQ STUFFH
	
	STA ENDCHR
	CMP #34
	BEQ STRNG
	
	BIT DORES
	BVS STUFFH
	
	CMP #'?'
	BNE KLOOP1
	
	LDA #PRINTK
	BNE STUFFH
	
.KLOOP1
	CMP #'0'
	BCC MUSTCR
	
	CMP #60
	BCC STUFFH
	
.MUSTCR
	STY BUFPTR
	LDY #0
	STY COUNT
	DEY
	STX TXTPTR
	DEX
	
.RESER
	INY
	INX
	
.RESCON
	LDA BUFOFS,X
	SEC
	SBC RESLST,Y
	BEQ RESER
	
	CMP #128
	BNE NTHIS
	ORA COUNT
	
.GETBPT
	LDY BUFPTR
	
.STUFFH
	INX
	INY
	STA BUF-5,Y
	LDA BUF-5,Y
	BEQ CRDONE
	
	SEC
	SBC #':'
	BEQ COLIS
	
	CMP #DATATK-$3A
	BNE NODATT
	
.COLIS
	STA DORES
	
.NODATT
	SEC
	SBC #REMTK-$3A
	BNE KLOOP
	
	STA ENDCHR
	
.STR1
	LDA BUFOFS,X
	BEQ STUFFH
	
	CMP ENDCHR
	BEQ STUFFH
	
.STRNG
	INY
	STA BUF-5,Y
	INX
	BNE STR1
	
.NTHIS
	LDX TXTPTR
	INC COUNT

	;LOOK FOR END OF RESERVED WORD
.NTHIS1	INY
	LDA RESLST-1,Y
	BPL NTHIS1

	;LOOK FOR END OF RESLST
	LDA RESLST,Y
	BNE RESCON
	LDA BUFOFS,X
	BPL GETBPT

.CRDONE
	STA BUF-3,Y
	DEC TXTPTR+1
	
	ZZ1	=BUF-1

	LDA #<ZZ1
	STA TXTPTR
	RTS
	
.FNDLIN
	LDA TXTTAB
	LDX TXTTAB+1
	
.FNDLNC
	LDY #1
	STA LOWTR
	STX LOWTR+1
	LDA (LOWTR),Y
	BEQ FLINRT
	
	INY
	INY
	LDA LINNUM+1
	CMP (LOWTR),Y
	BCC FLNRTS
	BEQ FNDLO1
	
	DEY
	BNE AFFRTS
	
.FNDLO1
	LDA LINNUM
	DEY
	CMP (LOWTR),Y
	BCC FLNRTS
	BEQ FLNRTS
	
.AFFRTS
	DEY
	LDA (LOWTR),Y
	TAX
	DEY
	LDA (LOWTR),Y
	BCS FNDLNC
	
.FLINRT
	CLC
	
.FLNRTS
	RTS
	
.SCRATH
	BNE FLNRTS
	
.SCRTCH
	LDA #0
	TAY
	STA (TXTTAB),Y
	INY
	STA (TXTTAB),Y
	LDA TXTTAB
	CLC
	ADC #2
	STA VARTAB
	LDA TXTTAB+1
	ADC #0
	STA VARTAB+1
	
.RUNC
	JSR STXTPT
	LDA #0
	
.CLEAR
	BNE STKRTS
	
.CLEARC
	LDA MEMSIZ
	LDY MEMSIZ+1
	STA FRETOP
	STY FRETOP+1
	JSR CCALL
	LDA VARTAB
	LDY VARTAB+1
	STA ARYTAB
	STY ARYTAB+1
	STA STREND
	STY STREND+1
	
.FLOAD
	JSR RESTOR
	
.STKINI
	LDX #TEMPST
	STX TEMPPT
	PLA
	TAY
	PLA
	LDX #STKEND-257
	TXS
	PHA
	TYA
	PHA
	LDA #0
	STA OLDTXT+1
	STA SUBFLG
	
.STKRTS
	RTS
	
.STXTPT
	CLC
	LDA TXTTAB
	ADC #255
	STA TXTPTR
	LDA TXTTAB+1
	ADC #255
	STA TXTPTR+1
	RTS

.LIST
	BCC GOLST
	BEQ GOLST
	
	CMP #MINUTK
	BNE STKRTS
	
.GOLST
	JSR LINGET
	JSR FNDLIN
	JSR CHRGOT
	BEQ LSTEND
	
	CMP #MINUTK
	BNE FLNRTS
	
	JSR CHRGET
	JSR LINGET
	BNE FLNRTS
	
.LSTEND
	PLA
	PLA
	LDA LINNUM
	ORA LINNUM+1
	BNE LIST4
	
	LDA #255
	STA LINNUM
	STA LINNUM+1
	
.LIST4
	LDY #1
	STY DORES
	LDA (LOWTR),Y
	BEQ GRODY
	
	JSR ISCNTC
	JSR CRDO
	INY
	LDA (LOWTR),Y
	TAX
	INY
	LDA (LOWTR),Y
	CMP LINNUM+1
	BNE TSTDUN
	
	CPX LINNUM
	BEQ TYPLIN
	
.TSTDUN
	BCS GRODY
	
.TYPLIN
	STY LSTPNT
	JSR LINPRT
	LDA #' '
	
.PRIT4
	LDY LSTPNT
	AND #127
	
.PLOOP
	JSR OUTDO
	CMP #34
	BNE PLOOP1
	
	LDA DORES
	EOR #$FF
	STA DORES
	
.PLOOP1
	INY
	BEQ GRODY
	
	LDA (LOWTR),Y
	BNE QPLOP
	
	TAY
	LDA (LOWTR),Y
	TAX
	INY
	LDA (LOWTR),Y
	STX LOWTR
	STA LOWTR+1
	BNE LIST4
	
.GRODY
	JMP READY
	
.QPLOP
	BPL PLOOP
	
	CMP #XPI
	BEQ PLOOP
	
	BIT DORES
	BMI PLOOP
	
	SEC
	SBC #127
	TAX
	STY LSTPNT
	LDY #255
	
.RESRCH
	DEX
	BEQ PRIT3
	
.RESCR1
	INY
	LDA RESLST,Y
	BPL RESCR1
	BMI RESRCH
	
.PRIT3
	INY
	LDA RESLST,Y
	BMI PRIT4
	
	JSR OUTDO
	BNE PRIT3
	
.FOR
	LDA #128
	STA SUBFLG
	JSR LET
	JSR FNDFOR
	BNE NOTOL
	
	TXA
	ADC #FORSIZ-3
	TAX
	TXS
	
.NOTOL
	PLA
	PLA
	LDA #8+ADDPRC
	JSR GETSTK
	JSR DATAN
	CLC
	TYA
	ADC TXTPTR
	PHA
	LDA TXTPTR+1
	ADC #0
	PHA
	LDA CURLIN+1
	PHA
	LDA CURLIN
	PHA
	LDA #TOTK
	JSR SYNCHR
	JSR CHKNUM
	JSR FRMNUM
	LDA FACSGN
	ORA #127
	AND FACHO
	STA FACHO
	LDA #<LDFONE
	LDY #>LDFONE
	STA INDEX1
	STY INDEX1+1
	JMP FORPSH
	
.LDFONE
	LDA #<FONE
	LDY #>FONE
	JSR MOVFM
	JSR CHRGOT
	CMP #STEPTK
	BNE ONEON
	
	JSR CHRGET
	JSR FRMNUM
	
.ONEON
	JSR SIGN
	JSR PUSHF
	LDA FORPNT+1
	PHA
	LDA FORPNT
	PHA
	LDA #FORTK
	PHA

.NEWSTT
	JSR ISCNTC
	LDA TXTPTR
	LDY TXTPTR+1
	CPY #BUFPAG
	NOP
	BEQ DIRCON
	
	STA OLDTXT
	STY OLDTXT+1
	
.DIRCON
	LDY #0
	LDA (TXTPTR),Y
	BNE MORSTS
	
	LDY #2
	LDA (TXTPTR),Y
	CLC
	BNE DIRCN1
	
	JMP ENDCON
	
.DIRCN1
	INY
	LDA (TXTPTR),Y
	STA CURLIN
	INY
	LDA (TXTPTR),Y
	STA CURLIN+1
	TYA
	ADC TXTPTR
	STA TXTPTR
	BCC GONE
	
	INC TXTPTR+1
	
.GONE
	JSR CHRGET
	JSR GONE3
	JMP NEWSTT
	
.GONE3
	BEQ ISCRTS
	
.GONE2
	SBC #ENDTK
	BCC GLET
	
	CMP #SCRATK-ENDTK+1
	BCS SNERRX
	
	ASL A
	TAY
	LDA STMDSP+1,Y 
	PHA
	LDA STMDSP,Y
	PHA
	JMP CHRGET
	
.GLET
	JMP LET
	
.MORSTS
	CMP #':'
	BEQ GONE
	
.SNERR1
	JMP SNERR

.SNERRX
	CMP #GOTK-ENDTK
	BNE SNERR1
	
	JSR CHRGET
	LDA #TOTK
	JSR SYNCHR
	JMP GOTO
	
.RESTOR
	SEC
	LDA TXTTAB
	SBC #1
	LDY TXTTAB+1
	BCS RESFIN
	
	DEY
	
.RESFIN
	STA DATPTR
	STY DATPTR+1
	
.ISCRTS
	RTS
	
.STOP
.BSTOP
	BCS STOPC
	
.END
	CLC
	
.STOPC			; IF C==0 GO READY
	BNE CONTRT
	
	LDA TXTPTR
	LDY TXTPTR+1
	LDX CURLIN+1
	INX
	BEQ DIRIS
	
	STA OLDTXT
	STY OLDTXT+1
	
.STPEND
	LDA CURLIN
	LDY CURLIN+1
	STA OLDLIN
	STY OLDLIN+1
	
.DIRIS
	PLA
	PLA
	
.ENDCON
	LDA #<BRKTXT
	LDY #>BRKTXT
	LDX #0
	STX CNTWFL
	BCC GORDY
	
	JMP ERRFIN
	
.GORDY
	JMP READY
	
.CONT
	BNE CONTRT
	
	LDX #ERRCN
	LDY OLDTXT+1
	BNE *+5
	
	JMP ERROR
	
	LDA OLDTXT
	STA TXTPTR
	STY TXTPTR+1
	LDA OLDLIN
	LDY OLDLIN+1
	STA CURLIN
	STY CURLIN+1
	
.CONTRT
	RTS
	
.RUN
	BNE *+5
	
	JMP RUNC
	
	JSR CLEARC
	JMP RUNC2
	
.GOSUB
	LDA #3
	JSR GETSTK
	LDA TXTPTR+1
	PHA
	LDA TXTPTR
	PHA
	LDA CURLIN+1
	PHA
	LDA CURLIN
	PHA
	LDA #GOSUTK
	PHA
	
.RUNC2
	JSR CHRGOT
	JSR GOTO
	JMP NEWSTT
	
.GOTO
	JSR LINGET
	JSR REMN
	LDA CURLIN+1
	CMP LINNUM+1
	BCS LUK4IT
	
	TYA
	SEC
	ADC TXTPTR
	LDX TXTPTR+1
	BCC LUKALL
	
	INX
	BCS LUKALL
	
.LUK4IT
	LDA TXTTAB
	LDX TXTTAB+1
	
.LUKALL
	JSR FNDLNC
	BCC USERR
	
	LDA LOWTR
	SBC #1
	STA TXTPTR
	LDA LOWTR+1
	SBC #0
	STA TXTPTR+1
	
.GORTS
	RTS
	
.RETURN
	BNE GORTS
	
	LDA #255
	STA FORPNT+1
	JSR FNDFOR
	TXS
	CMP #GOSUTK
	BEQ RETU1
	
	LDX #ERRRG
	HOP2
	
.USERR
	LDX #ERRUS
	JMP ERROR
	
.SNERR2
	JMP SNERR
	
.RETU1
	PLA
	PLA
	STA CURLIN
	PLA
	STA CURLIN+1
	PLA
	STA TXTPTR
	PLA
	STA TXTPTR+1
	
.DATA
	JSR DATAN
	
.ADDON
	TYA
	CLC
	ADC TXTPTR
	STA TXTPTR
	BCC REMRTS
	
	INC TXTPTR+1
	
.REMRTS
	RTS
	
.DATAN
	LDX #':'
	HOP2
	
.REMN
	LDX #0
	STX CHARAC
	LDY #0
	STY ENDCHR
	
.EXCHQT
	LDA ENDCHR
	LDX CHARAC
	STA CHARAC
	STX ENDCHR
	
.REMER
	LDA (TXTPTR),Y
	BEQ REMRTS
	
	CMP ENDCHR
	BEQ REMRTS
	
	INY
	CMP #34
	BNE REMER
	BEQ EXCHQT
	
.IF
	JSR FRMEVL
	JSR CHRGOT
	CMP #GOTOTK
	BEQ OKGOTO
	
	LDA #THENTK
	JSR SYNCHR
	
.OKGOTO
	LDA FACEXP
	BNE DOCOND
	
.REM
	JSR REMN
	BEQ ADDON
	
.DOCOND
	JSR CHRGOT
	BCS DOCO
	
	JMP GOTO
	
.DOCO
	JMP GONE3
	
.ONGOTO
	JSR GETBYT
	PHA
	CMP #GOSUTK
	BEQ ONGLOP
	
.SNERR3
	CMP #GOTOTK
	BNE SNERR2
	
.ONGLOP
	DEC FACLO
	BNE ONGLP1
	
	PLA
	JMP GONE2
	
.ONGLP1
	JSR CHRGET
	JSR LINGET
	CMP #44
	BEQ ONGLOP
	
	PLA
	
.ONGRTS
	RTS
	
.LINGET
	LDX #0
	STX LINNUM
	STX LINNUM+1
	
.MORLIN
	BCS ONGRTS
	
	SBC #$2F
	STA CHARAC
	LDA LINNUM+1
	STA INDEX
	CMP #25
	BCS SNERR3
	
	LDA LINNUM
	ASL A
	ROL INDEX
	ASL A
	ROL INDEX
	ADC LINNUM
	STA LINNUM
	LDA INDEX
	ADC LINNUM+1
	STA LINNUM+1
	ASL LINNUM
	ROL LINNUM+1
	LDA LINNUM
	ADC CHARAC
	STA LINNUM
	BCC NXTLGC
	
	INC LINNUM+1
	
.NXTLGC
	JSR CHRGET
	JMP MORLIN
	
.LET
	JSR PTRGET
	STA FORPNT
	STY FORPNT+1
	LDA #$B2
	JSR SYNCHR
	LDA INTFLG
	PHA
	LDA VALTYP
	PHA
	JSR FRMEVL
	PLA
	ROL A
	JSR CHKVAL
	BNE COPSTR
	PLA
	
.QINTGR
	BPL COPFLT
	
	JSR ROUND
	JSR AYINT
	LDY #0
	LDA FACMO
	STA (FORPNT),Y
	INY
	LDA FACLO
	STA (FORPNT),Y
	RTS
	
.COPFLT
	JMP MOVVF
	
.COPSTR
	PLA
	
.INPCOM
	LDY FORPNT+1
	CPY #>ZERO
	BNE GETSPT
	
	JSR FREFAC
	CMP #6
	BNE FCERR2
	
	LDY #0
	STY FACEXP
	STY FACSGN

.TIMELP
	STY FBUFPT
	JSR TIMNUM
	JSR MUL10
	INC FBUFPT
	LDY FBUFPT
	JSR TIMNUM
	
	JSR MOVAF
	TAX
	BEQ NOML6

	INX
	TXA
	JSR FINML6

.NOML6
	LDY FBUFPT
	INY
	CPY #6
	BNE TIMELP
	
	JSR MUL10
	JSR QINT

	LDX #2
	SEI
	
.TIMEST
	LDA FACMOH,X
	STA CTIMR,X
	DEX
	BPL TIMEST

	CLI
	RTS
	
.TIMNUM
	LDA (INDEX),Y
	JSR QNUM
	BCC GOTNUM
	
.FCERR2
	JMP FCERR
	
.GOTNUM
	SBC #$2F
	JMP FINLOG
	
.GETSPT
	LDY #2
	LDA (FACMO),Y
	CMP FRETOP+1
	BCC DNTCPY
	BNE QVARIA
	
	DEY
	LDA (FACMO),Y
	CMP FRETOP
	BCC DNTCPY
	
.QVARIA
	LDY FACLO
	CPY VARTAB+1
	BCC DNTCPY
	BNE COPY
	
	LDA FACMO
	CMP VARTAB
	BCS COPY
	
.DNTCPY
	LDA FACMO
	LDY FACMO+1
	JMP COPYC
	
.COPY
	LDY #0
	LDA (FACMO),Y
	JSR STRINI
	LDA DSCPNT
	LDY DSCPNT+1
	STA STRNG1
	STY STRNG1+1
	JSR MOVINS
	LDA #<DSCTMP
	LDY #>DSCTMP
	
.COPYC
	STA DSCPNT
	STY DSCPNT+1
	JSR FRETMS
	LDY #0
	LDA (DSCPNT),Y
	STA (LSTPNT),Y
	INY
	LDA (DSCPNT),Y
	STA (LSTPNT),Y
	INY
	LDA (DSCPNT),Y
	STA (LSTPNT),Y
	RTS

.PRINTN
	JSR CMD
	JMP IODONE
	
.CMD
	JSR GETBYT
	BEQ SAVEIT
	
	LDA #44
	JSR SYNCHR
	
.SAVEIT
	PHP
	JSR COOUT
	STX CHANNL
	PLP
	JMP PRINT
	
.STRDON
	JSR STRPRT
	
.NEWCHR
	JSR CHRGOT
	
.PRINT
	BEQ CRDO
	
.PRINTC
	BEQ PRTRTS
	
	CMP #TABTK
	BEQ TABER
	
	CMP #SPCTK
	CLC
	BEQ TABER
	
	CMP #44
	BEQ COMPRT
	
	CMP #59
	BEQ NOTABR
	
	JSR FRMEVL
	BIT VALTYP
	BMI STRDON
	
	JSR FOUT
	JSR STRLIT
	JSR STRPRT
	JSR OUTSPC
	BNE NEWCHR
	
.FININL
	LDA #0
	STA BUF,X
	
	ZZ5=BUF-1
	
	LDX #<ZZ5
	LDY #>ZZ5
	LDA CHANNL
	BNE PRTRTS
	
.CRDO
	LDA #13
	JSR OUTDO
	LDA #$0A        ;LINE FEED
	JSR OUTDO
	
.CRFIN
	EOR #255
	
.PRTRTS
	RTS
	
.COMPRT
	LDA TRMPOS
	
	NCMPOS	=36
	
	SEC
	
.MORCO1
	SBC #CLMWID
	BCS MORCO1
	
	EOR #255
	ADC #1
	BNE ASPAC
	
.TABER
	PHP
	JSR GTBYTC
	CMP #41
	BNE SNERR4
	
	PLP
	BCC XSPAC
	
	TXA
	SBC TRMPOS
	BCC NOTABR
	
.ASPAC
	TAX
	
.XSPAC
	INX
	
.XSPAC2
	DEX
	BNE XSPAC1
	
.NOTABR
	JSR CHRGET
	JMP PRINTC

.XSPAC1
	JSR OUTSPC
	BNE XSPAC2
	
.STROUT
	JSR STRLIT
	
.STRPRT
	JSR FREFAC
	TAX
	LDY #0
	INX
	
.STRPR2
	DEX
	BEQ PRTRTS
	
	LDA (INDEX),Y
	JSR OUTDO
	INY
	CMP #13
	BNE STRPR2
	
	JSR CRFIN
	JMP STRPR2
	
.OUTSPC
	LDA CHANNL
	BEQ CRTSKP
	
	LDA #' '
	HOP2
	
.CRTSKP
	LDA #29
	HOP2
	
.OUTQST
	LDA #'?'
	
.OUTDO
	BIT CNTWFL
	BMI OUTRTS
	
	JSR OUTCH
	
.OUTRTS
	AND #255
	RTS
	
.TRMNOK
	LDA INPFLG
	BEQ TRMNO1
	BMI GETDTL
	
	LDY #255
	BNE STCURL
	
.GETDTL
	LDA DATLIN
	LDY DATLIN+1
	
.STCURL
	STA CURLIN
	STY CURLIN+1
	
.SNERR4
	JMP SNERR
	
.TRMNO1
	LDA CHANNL
	BEQ DOAGIN
	
	LDX #ERRBD
	JMP ERROR
	
.DOAGIN
	LDA #<TRYAGN
	LDY #>TRYAGN
	JSR STROUT
	LDA OLDTXT
	LDY OLDTXT+1
	STA TXTPTR
	STY TXTPTR+1
	RTS
	
.GET
	JSR ERRDIR
	CMP #'#'
	BNE GETTTY
	
	JSR CHRGET
	JSR GETBYT
	LDA #44
	JSR SYNCHR
	JSR COIN
	STX CHANNL
	
	ZZ2=BUF+1
	
.GETTTY
	LDX #<ZZ2
	
	ZZ3=BUF+2
	
	LDY #>ZZ3
	LDA #0
	STA BUF+1
	LDA #64
	JSR INPCO1
	LDX CHANNL
	BNE IORELE
	
	RTS
	
.INPUTN
	JSR GETBYT
	LDA #44
	JSR SYNCHR
	JSR COIN
	STX CHANNL
	JSR NOTQTI
	
.IODONE
	LDA CHANNL
	
.IORELE
	JSR CCCHN
	LDX #0
	STX CHANNL
	RTS
	
.INPUT
	LSR CNTWFL
	CMP #34
	BNE NOTQTI
	
	JSR STRTXT
	LDA #59
	JSR SYNCHR
	JSR STRPRT
	
.NOTQTI
	JSR ERRDIR
	LDA #44
	STA BUF-1
	
.GETAGN
	JSR QINLIN
	LDA CHANNL
	BEQ BUFFUL
	
	LDA CSTAT
	AND #2
	BEQ BUFFUL
	
	JSR IODONE
	JMP DATA
	
.BUFFUL
	LDA BUF
	BNE INPCON
	
	LDA CHANNL
	BNE GETAGN
	
	CLC
	JMP STPEND
	
.QINLIN
	LDA CHANNL
	BNE GINLIN
	JSR OUTQST
	JSR OUTSPC
	
.GINLIN
	JMP INLIN
	
.BREAD
	LDX DATPTR
	LDY DATPTR+1
	EQUB $A9
	TYA
	HOP2
	
.INPCON
	LDA #0
	
.INPCO1
	STA INPFLG
	STX INPPTR
	STY INPPTR+1
	
.INLOOP
	JSR PTRGET
	STA FORPNT
	STY FORPNT+1
	LDA TXTPTR
	LDY TXTPTR+1
	STA VARTXT
	STY VARTXT+1
	LDX INPPTR
	LDY INPPTR+1
	STX TXTPTR
	STY TXTPTR+1
	JSR CHRGOT
	BNE DATBK1
	
	BIT INPFLG
	BVC QDATA
	
	JSR CGETL
	STA BUF
	
	ZZ4=BUF-1
	
	LDX #<ZZ4
	LDY #>ZZ4
	BNE DATBK
	
.QDATA
	BMI DATLOP
	LDA CHANNL
	BNE GETNTH
	
	JSR OUTQST
	
.GETNTH
	JSR QINLIN
	
.DATBK
	STX TXTPTR
	STY TXTPTR+1

.DATBK1
	JSR CHRGET
	BIT VALTYP
	BPL NUMINS
	
	BIT INPFLG
	BVC SETQUT
	
	INX
	STX TXTPTR
	LDA #0
	STA CHARAC
	BEQ RESETC
	
.SETQUT
	STA CHARAC
	CMP #34
	BEQ NOWGET
	
	LDA #':'
	STA CHARAC
	LDA #44
	
.RESETC
	CLC
	
.NOWGET
	STA ENDCHR
	LDA TXTPTR
	LDY TXTPTR+1
	ADC #0
	BCC NOWGE1
	
	INY
	
.NOWGE1
	JSR STRLT2
	JSR ST2TXT
	JSR INPCOM
	JMP STRDN2
	
.NUMINS
	JSR FIN
	LDA INTFLG
	JSR QINTGR
	
.STRDN2
	JSR CHRGOT
	BEQ TRMOK
	
	CMP #44
	BEQ *+5
	
	JMP TRMNOK
	
.TRMOK
	LDA TXTPTR
	LDY TXTPTR+1
	STA INPPTR
	STY INPPTR+1
	LDA VARTXT
	LDY VARTXT+1
	STA TXTPTR
	STY TXTPTR+1
	JSR CHRGOT
	BEQ VAREND
	
	JSR CHKCOM
	JMP INLOOP
	
.DATLOP
	JSR DATAN
	INY
	TAX
	BNE NOWLIN
	
	LDX #ERROD
	INY
	LDA (TXTPTR),Y
	BEQ ERRGO5
	
	INY
	LDA (TXTPTR),Y
	STA DATLIN
	INY
	LDA (TXTPTR),Y
	INY
	STA DATLIN+1
	
.NOWLIN
	LDA (TXTPTR),Y
	TAX
	JSR ADDON
	CPX #DATATK
	BNE DATLOP
	
	JMP DATBK1
	
.VAREND
	LDA INPPTR
	LDY INPPTR+1
	LDX INPFLG
	BPL VARY0
	
	JMP RESFIN
	
.VARY0
	LDY #0
	LDA (INPPTR),Y
	BEQ INPRTS
	LDA CHANNL
	BNE INPRTS
	LDA #<EXIGNT
	LDY #>EXIGNT
	JMP STROUT
	
.INPRTS
	RTS

.EXIGNT
	EQUS "?EXTRA IGNORED"
	EQUB $D,$A
	EQUB 0
	
.TRYAGN
	EQUS "?REDO FROM START"
	EQUB $D,$A
	EQUB 0
	
.NEXT
	BNE GETFOR
	
	LDY #0
	BEQ STXFOR
	
.GETFOR
	JSR PTRGET
	
.STXFOR
	STA FORPNT
	STY FORPNT+1
	JSR FNDFOR
	BEQ HAVFOR
	
	LDX #ERRNF
	
.ERRGO5
	BEQ ERRGO4
	
.HAVFOR
	TXS
	TXA
	CLC
	ADC #4
	PHA
	ADC #5+ADDPRC
	STA INDEX2
	PLA
	LDY #1
	JSR MOVFM
	TSX
	LDA ADDPRC+264,X
	STA FACSGN
	LDA FORPNT
	LDY FORPNT+1
	JSR FADD
	JSR MOVVF
	LDY #1
	JSR FCOMPN
	TSX
	SEC
	SBC ADDPRC+264,X
	BEQ LOOPDN
	
	LDA 269+ADDPRC+ADDPRC,X
	STA CURLIN
	LDA 270+ADDPRC+ADDPRC,X
	STA CURLIN+1
	LDA 272+ADDPRC+ADDPRC,X
	STA TXTPTR
	LDA 271+ADDPRC+ADDPRC,X
	STA TXTPTR+1
	
.NEWSGO
	JMP NEWSTT
	
.LOOPDN
	TXA
	ADC #15+ADDPRC+ADDPRC
	TAX
	TXS
	JSR CHRGOT
	CMP #44
	BNE NEWSGO
	
	JSR CHRGET
	JSR GETFOR
	
.FRMNUM
	JSR FRMEVL
	
.CHKNUM
	CLC
	HOP1
	
.CHKSTR
	SEC
	
.CHKVAL
	BIT VALTYP
	BMI DOCSTR
	BCS CHKERR
	
.CHKOK
	RTS
	
.DOCSTR
	BCS CHKOK
	
.CHKERR
	LDX #ERRTM
	
.ERRGO4
	JMP ERROR
	
.FRMEVL
	LDX TXTPTR
	BNE FRMEV1
	
	DEC TXTPTR+1
	
.FRMEV1
	DEC TXTPTR
	LDX #0
	HOP1
	
.LPOPER
	PHA
	TXA
	PHA
	LDA #1
	JSR GETSTK
	JSR EVAL
	LDA #0
	STA OPMASK
	
.TSTOPX
	JSR CHRGOT
	
.LOPREL
	SEC
	SBC #GREATK
	BCC ENDREL
	
	CMP #LESSTK-GREATK+1
	BCS ENDREL
	
	CMP #1
	ROL A
	EOR #1
	EOR OPMASK
	CMP OPMASK
	BCC SNERR5
	STA OPMASK
	JSR CHRGET
	JMP LOPREL
	
.ENDREL
	LDX OPMASK
	BNE FINREL
	BCS QOP
	
	ADC #GREATK-PLUSTK
	BCC QOP
	ADC VALTYP
	BNE *+5
	
	JMP CAT
	
	ADC #$FF
	STA INDEX1
	ASL A
	ADC INDEX1
	TAY
	
.QPREC
	PLA
	CMP OPTAB,Y
	BCS QCHNUM
	
	JSR CHKNUM
	
.DOPREC
	PHA
	
.NEGPRC
	JSR DOPRE1
	PLA
	LDY OPPTR
	BPL QPREC1
	
	TAX
	BEQ QOPGO
	BNE PULSTK
	
.FINREL
	LSR VALTYP
	TXA
	ROL A
	LDX TXTPTR
	BNE FINRE2
	
	DEC TXTPTR+1
	
.FINRE2
	DEC TXTPTR
	LDY #PTDORL-OPTAB
	STA OPMASK
	BNE QPREC
	
.QPREC1
	CMP OPTAB,Y
	BCS PULSTK
	BCC DOPREC
	
.DOPRE1
	LDA OPTAB+2,Y
	PHA
	LDA OPTAB+1,Y
	PHA
	JSR PUSHF1
	LDA OPMASK
	JMP LPOPER
	
.SNERR5
	JMP SNERR
	
.PUSHF1
	LDA FACSGN
	LDX OPTAB,Y
	
.PUSHF
	TAY
	PLA
	STA INDEX1
	INC INDEX1
	PLA
	STA INDEX1+1
	TYA
	PHA
	
.FORPSH
	JSR ROUND
	LDA FACLO
	PHA
	LDA FACMO
	PHA
	LDA FACMOH
	PHA
	LDA FACHO
	PHA
	LDA FACEXP
	PHA
	JMP (INDEX1)
	
.QOP
	LDY #255
	PLA
	
.QOPGO
	BEQ QOPRTS
	
.QCHNUM
	CMP #100
	BEQ UNPSTK
	
	JSR CHKNUM
	
.UNPSTK
	STY OPPTR
	
.PULSTK
	PLA
	LSR A
	STA DOMASK
	PLA
	STA ARGEXP
	PLA
	STA ARGHO
	PLA
	STA ARGMOH
	PLA
	STA ARGMO
	PLA
	STA ARGLO
	PLA
	STA ARGSGN
	EOR FACSGN
	STA ARISGN
	
.QOPRTS
	LDA FACEXP
	
.UNPRTS
	RTS
	
.EVAL
	LDA #0
	STA VALTYP
	
.EVAL0
	JSR CHRGET
	BCS EVAL2
	
.EVAL1
	JMP FIN
	
.EVAL2
	JSR ISLETC
	BCS ISVAR
	
	CMP #XPI
	BNE QDOT
	
	LDA #<PIVAL
	LDY #>PIVAL
	JSR MOVFM
	JMP CHRGET
	
.PIVAL
	EQUB $82
	EQUB $49
	EQUB $F
	EQUB $DA
	EQUB $A1
	
.QDOT
	CMP #'.'
	BEQ EVAL1
	
	CMP #MINUTK
	BEQ DOMIN
	
	CMP #PLUSTK
	BEQ EVAL0
	
	CMP #34
	BNE EVAL3
	
.STRTXT
	LDA TXTPTR
	LDY TXTPTR+1
	ADC #0
	BCC STRTX2
	
	INY
	
.STRTX2
	JSR STRLIT
	JMP ST2TXT
	
.EVAL3
	CMP #NOTTK
	BNE EVAL4
	
	LDY #24
	BNE GONPRC
	
.NOTOP
	JSR AYINT
	LDA FACLO
	EOR #255
	TAY
	LDA FACMO
	EOR #255
	JMP GIVAYF
	
.EVAL4
	CMP #FNTK
	BNE *+5
	
	JMP FNDOER
	
	CMP #ONEFUN
	BCC PARCHK
	
	JMP ISFUN
	
.PARCHK
	JSR CHKOPN
	JSR FRMEVL
	
.CHKCLS
	LDA #41
	HOP2
	
.CHKOPN
	LDA #40
	HOP2
	
.CHKCOM
	LDA #44
	
.SYNCHR
	LDY #0
	CMP (TXTPTR),Y
	BNE SNERR
	
	JMP CHRGET
	
.SNERR
	LDX #ERRSN
	JMP ERROR
	
.DOMIN
	LDY #21
	
.GONPRC
	PLA
	PLA
	JMP NEGPRC

.ISVAR
	JSR PTRGET
	
.ISVRET
	STA FACMO
	STY FACLO
	
	LDA VARNAM
	LDY VARNAM+1
	LDX VALTYP
	BEQ GOOO
	
	LDX #0
	STX FACOV

IF NOT(BEEB)
	BIT FACLO
	BPL STRRTS		; IF NOT IN ROM?
ENDIF
	
	CMP #'T'
	BNE STRRTS
	
	CPY #$C9
	BNE STRRTS
	
	JSR GETTIM
	STY TENEXP
	DEY
	STY FBUFPT
	LDY #6
	STY DECCNT
	LDY #FDCEND-FOUTBL
	JSR FOUTIM
	JMP TIMSTR
	
.STRRTS
	RTS
	
.GOOO
	LDX INTFLG
	BPL GOOOOO
	
	LDY #0
	LDA (FACMO),Y
	TAX
	INY
	LDA (FACMO),Y
	TAY
	TXA
	JMP GIVAYF
	
.GOOOOO
IF NOT(BEEB)
	BIT FACLO
	BPL GOMOVF		;IF NOT IN ROM?
ENDIF
	
	CMP #'T'
	BNE QSTATV
	
	CPY #'I'
	BNE GOMOVF
	
	JSR GETTIM
	TYA
	LDX #160
	JMP FLOATB
	
	ZZ7=CTIMR-2
	
.GETTIM
	LDA #<ZZ7
	LDY #>ZZ7
	SEI
	JSR MOVFM
	CLI
	STY FACHO
	RTS
	
.QSTATV
	CMP #'S'
	BNE GOMOVF
	
	CPY #'T'
	BNE GOMOVF
	
	LDA CSTAT
	JMP FLOAT
	
.GOMOVF
	LDA FACMO
	LDY FACMO+1
	JMP MOVFM
	
.ISFUN
	ASL A
	PHA
	TAX
	JSR CHRGET
	CPX #LASNUM+LASNUM-255
	BCC OKNORM
	
	JSR CHKOPN
	JSR FRMEVL
	JSR CHKCOM
	JSR CHKSTR
	PLA
	TAX
	LDA FACMO+1
	PHA
	LDA FACMO
	PHA
	TXA
	PHA
	JSR GETBYT
	PLA
	TAY
	TXA
	PHA
	JMP FINGO
	
.OKNORM
	JSR PARCHK
	PLA
	TAY
	
.FINGO
	LDA FUNDSP-ONEFUN-ONEFUN+256,Y
	STA JMPER+1
	LDA FUNDSP-ONEFUN-ONEFUN+257,Y
	STA JMPER+2
	JSR JMPER
	JMP CHKNUM
	
.OROP
	LDY #255
	HOP2
	
.ANDOP
	LDY #0
	STY COUNT
	JSR AYINT
	LDA FACMO
	EOR COUNT
	STA INTEGR
	LDA FACLO
	EOR COUNT
	STA INTEGR+1
	JSR MOVFA
	JSR AYINT
	LDA FACLO
	EOR COUNT
	AND INTEGR+1
	EOR COUNT
	TAY
	LDA FACMO
	EOR COUNT
	AND INTEGR
	EOR COUNT
	JMP GIVAYF
	
.DOREL
	JSR CHKVAL
	BCS STRCMP
	
	LDA ARGSGN
	ORA #127
	AND ARGHO
	STA ARGHO
	LDA #<ARGEXP
	LDY #>ARGEXP
	JSR FCOMP
	TAX
	JMP QCOMP
	
.STRCMP
	LDA #0
	STA VALTYP
	DEC OPMASK
	JSR FREFAC
	STA DSCTMP
	STX DSCTMP+1
	STY DSCTMP+2
	LDA ARGMO
	LDY ARGMO+1
	JSR FRETMP
	STX ARGMO
	STY ARGMO+1
	TAX
	SEC
	SBC DSCTMP
	BEQ STASGN
	
	LDA #1
	BCC STASGN
	LDX DSCTMP
	LDA #$FF
	
.STASGN
	STA FACSGN
	LDY #255
	INX
	
.NXTCMP
	INY
	DEX
	BNE GETCMP
	
	LDX FACSGN
	
.QCOMP
	BMI DOCMP
	CLC
	BCC DOCMP
	
.GETCMP
	LDA (ARGMO),Y
	CMP (DSCTMP+1),Y
	BEQ NXTCMP
	
	LDX #$FF
	BCS DOCMP
	
	LDX #1
	
.DOCMP
	INX
	TXA
	ROL A
	AND DOMASK
	BEQ GOFLOT
	
	LDA #$FF
	
.GOFLOT
	JMP FLOAT
	
.DIM3
	JSR CHKCOM
	
.DIM
	TAX
	JSR PTRGT1
	JSR CHRGOT
	BNE DIM3
	
	RTS
	
.PTRGET
	LDX #0
	JSR CHRGOT
	
.PTRGT1
	STX DIMFLG
	
.PTRGT2
	STA VARNAM
	JSR CHRGOT
	JSR ISLETC
	BCS PTRGT3
	
.INTERR
	JMP SNERR
	
.PTRGT3
	LDX #0
	STX VALTYP
	STX INTFLG
	JSR CHRGET
	BCC ISSEC
	
	JSR ISLETC
	BCC NOSEC
	
.ISSEC
	TAX
	
.EATEM
	JSR CHRGET
	BCC EATEM
	
	JSR ISLETC
	BCS EATEM
	
.NOSEC
	CMP #'$'
	BNE NOTSTR
	
	LDA #$FF
	STA VALTYP
	BNE TURNON
	
.NOTSTR
	CMP #'%'
	BNE STRNAM
	
	LDA SUBFLG
	BNE INTERR
	
	LDA #128
	STA INTFLG
	ORA VARNAM
	STA VARNAM
	
.TURNON
	TXA
	ORA #128
	TAX
	JSR CHRGET
	
.STRNAM
	STX VARNAM+1
	SEC
	ORA SUBFLG
	SBC #40
	BNE *+5
	
	JMP ISARY
	
	LDA #0
	STA SUBFLG
	LDA VARTAB
	LDX VARTAB+1
	LDY #0
	
.STXFND
	STX LOWTR+1
	
.LOPFND
	STA LOWTR
	CPX ARYTAB+1
	BNE LOPFN
	
	CMP ARYTAB
	BEQ NOTFNS
	
.LOPFN
	LDA VARNAM
	CMP (LOWTR),Y
	BNE NOTIT
	
	LDA VARNAM+1
	INY
	CMP (LOWTR),Y
	BEQ FINPTR
	
.NXTPTR
	DEY
	
.NOTIT
	CLC
	LDA LOWTR
	ADC #6+ADDPRC
	BCC LOPFND
	
	INX
	BNE STXFND
	
.ISLETC
	CMP #'A'
	BCC ISLRTS
	SBC #$5B
	SEC
	SBC #$A5
	
.ISLRTS
	RTS
	
.NOTFNS
	PLA
	PHA
	
	ZZ6=ISVRET-1
	
	CMP #<ZZ6
	BNE NOTEVL		; IF NOT CALLED BY ISVRET
	
.LDZR
	LDA #<ZERO
	LDY #>ZERO
	RTS
	
.NOTEVL
	LDA VARNAM
	LDY VARNAM+1
	CMP #'T'
	BNE QSTAVR
	
	CPY #$C9
	BEQ LDZR
	
	CPY #$49
	BNE QSTAVR
	
.GOBADV
	JMP SNERR
	
.QSTAVR
	CMP #'S'
	BNE VAROK
	
	CPY #'T'
	BEQ GOBADV
	
.VAROK
	LDA ARYTAB
	LDY ARYTAB+1
	STA LOWTR
	STY LOWTR+1
	LDA STREND
	LDY STREND+1
	STA HIGHTR
	STY HIGHTR+1
	CLC
	ADC #6+ADDPRC
	BCC NOTEVE
	
	INY
	
.NOTEVE
	STA HIGHDS
	STY HIGHDS+1
	JSR BLTU
	LDA HIGHDS
	LDY HIGHDS+1
	INY
	STA ARYTAB
	STY ARYTAB+1
	
.ARYDON
	LDY #0
	LDA VARNAM
	STA (LOWTR),Y
	INY
	LDA VARNAM+1
	STA (LOWTR),Y
	LDA #0
	INY
	STA (LOWTR),Y
	INY
	STA (LOWTR),Y
	INY
	STA (LOWTR),Y
	INY
	STA (LOWTR),Y
	INY
	STA (LOWTR),Y
	
.FINPTR
	LDA LOWTR
	CLC
	ADC #2
	LDY LOWTR+1
	BCC FINNOW
	
	INY
	
.FINNOW
	STA VARPNT
	STY VARPNT+1
	RTS
	
.FMAPTR
	LDA COUNT
	ASL A
	ADC #5
	ADC LOWTR
	LDY LOWTR+1
	BCC JSRGM
	
	INY
	
.JSRGM
	STA ARYPNT
	STY ARYPNT+1
	RTS

.N32768
	EQUB 144,128,0,0
	
.INTIDX
	JSR CHRGET
	JSR FRMEVL
	
.POSINT
	JSR CHKNUM
	LDA FACSGN
	BMI NONONO
	
.AYINT
	LDA FACEXP
	CMP #144
	BCC QINTGO
	
	LDA #<N32768
	LDY #>N32768
	JSR FCOMP
	
.NONONO
	BNE FCERR
	
.QINTGO
	JMP QINT
	
.ISARY
	LDA DIMFLG
	ORA INTFLG
	PHA
	LDA VALTYP
	PHA
	LDY #0
	
.INDLOP
	TYA
	PHA
	LDA VARNAM+1
	PHA
	LDA VARNAM
	PHA
	JSR INTIDX
	PLA
	STA VARNAM
	PLA
	STA VARNAM+1
	PLA
	TAY
	TSX
	LDA 258,X
	PHA
	LDA 257,X
	PHA
	LDA INDICE
	STA 258,X
	LDA INDICE+1
	STA 257,X
	INY
	JSR CHRGOT
	CMP #44
	BEQ INDLOP
	
	STY COUNT
	JSR CHKCLS
	PLA
	STA VALTYP
	PLA
	STA INTFLG
	AND #127
	STA DIMFLG
	LDX ARYTAB
	LDA ARYTAB+1
	
.LOPFDA
	STX LOWTR
	STA LOWTR+1
	CMP STREND+1
	BNE LOPFDV
	
	CPX STREND
	BEQ NOTFDD
	
.LOPFDV
	LDY #0
	LDA (LOWTR),Y
	INY
	CMP VARNAM
	BNE NMARY1
	
	LDA VARNAM+1
	CMP (LOWTR),Y
	BEQ GOTARY
	
.NMARY1
	INY
	LDA (LOWTR),Y
	CLC
	ADC LOWTR
	TAX
	INY
	LDA (LOWTR),Y
	ADC LOWTR+1
	BCC LOPFDA
	
.BSERR
	LDX #ERRBS
	HOP2
	
.FCERR
	LDX #ERRFC
	
.ERRGO3	JMP ERROR

.GOTARY
	LDX #ERRDD
	LDA DIMFLG
	BNE ERRGO3
	
	JSR FMAPTR
	LDA COUNT
	LDY #4
	CMP (LOWTR),Y
	BNE BSERR
	
	JMP GETDEF
	
.NOTFDD
	JSR FMAPTR
	JSR REASON
	LDA #0
	TAY
	STA CURTOL+1
	LDX #5
	LDA VARNAM
	STA (LOWTR),Y
	BPL NOTFLT
	
	DEX
	
.NOTFLT
	INY
	LDA VARNAM+1
	STA (LOWTR),Y
	BPL STOMLT
	
	DEX
	DEX
	
.STOMLT	
STX CURTOL
	LDA COUNT
	INY
	INY
	INY
	STA (LOWTR),Y
	
.LOPPTA
	LDX #11
	LDA #0
	BIT DIMFLG
	BVC NOTDIM
	
	PLA
	CLC
	ADC #1
	TAX
	PLA
	ADC #0
	
.NOTDIM
	INY
	STA (LOWTR),Y
	INY
	TXA
	STA (LOWTR),Y
	JSR UMULT
	STX CURTOL
	STA CURTOL+1
	LDY INDEX
	DEC COUNT
	BNE LOPPTA
	
	ADC ARYPNT+1
	BCS OMERR1
	
	STA ARYPNT+1
	TAY
	TXA
	ADC ARYPNT
	BCC GREASE
	
	INY
	BEQ OMERR1
	
.GREASE
	JSR REASON
	STA STREND
	STY STREND+1
	LDA #0
	INC CURTOL+1
	LDY CURTOL
	BEQ DECCUR
	
.ZERITA
	DEY
	STA (ARYPNT),Y
	BNE ZERITA
	
.DECCUR
	DEC ARYPNT+1
	DEC CURTOL+1
	BNE ZERITA
	
	INC ARYPNT+1
	SEC
	LDA STREND
	SBC LOWTR
	LDY #2
	STA (LOWTR),Y
	LDA STREND+1
	INY
	SBC LOWTR+1
	STA (LOWTR),Y
	LDA DIMFLG
	BNE DIMRTS
	
	INY
	
.GETDEF
	LDA (LOWTR),Y
	STA COUNT
	LDA #0
	STA CURTOL
	
.INLPNM
	STA CURTOL+1
	INY
	PLA
	TAX
	STA INDICE
	PLA
	STA INDICE+1
	CMP (LOWTR),Y
	BCC INLPN2
	BNE BSERR7
	
	INY
	TXA
	CMP (LOWTR),Y
	BCC INLPN1
	
.BSERR7
	JMP BSERR
	
.OMERR1
	JMP OMERR

.INLPN2
	INY

.INLPN1
	LDA CURTOL+1
	ORA CURTOL
	CLC
	BEQ ADDIND
	
	JSR UMULT
	TXA
	ADC INDICE
	TAX
	TYA
	LDY INDEX1
	
.ADDIND
	ADC INDICE+1
	STX CURTOL
	DEC COUNT
	BNE INLPNM
	
	STA CURTOL+1
	LDX #5
	LDA VARNAM
	BPL NOTFL1
	
	DEX
	
.NOTFL1
	LDA VARNAM+1
	BPL STOML1
	
	DEX
	DEX
	
.STOML1
	STX ADDEND
	LDA #0
	JSR UMULTD
	TXA
	ADC ARYPNT
	STA VARPNT
	TYA
	ADC ARYPNT+1
	STA VARPNT+1
	TAY
	LDA VARPNT
	
.DIMRTS	RTS

.UMULT
	STY INDEX
	LDA (LOWTR),Y
	STA ADDEND
	DEY
	LDA (LOWTR),Y
	
.UMULTD
	STA ADDEND+1
	LDA #16
	STA DECCNT
	LDX #0
	LDY #0
	
.UMULTC
	TXA
	ASL A
	TAX
	TYA
	ROL A
	TAY
	BCS OMERR1
	
	ASL CURTOL
	ROL CURTOL+1
	BCC UMLCNT
	
	CLC
	TXA
	ADC ADDEND
	TAX
	TYA
	ADC ADDEND+1
	TAY
	BCS OMERR1
	
.UMLCNT
	DEC DECCNT
	BNE UMULTC
	
.UMLRTS
	RTS

.FRE
	LDA VALTYP
	BEQ NOFREF
	JSR FREFAC
	
.NOFREF	
	JSR GARBA2
	SEC
	LDA FRETOP
	SBC STREND
	TAY
	LDA FRETOP+1
	SBC STREND+1
	
.GIVAYF
	LDX #0
	STX VALTYP
	STA FACHO
	STY FACHO+1
	LDX #144
	JMP FLOATS
	
.POS
	LDY TRMPOS

.SNGFLT
	LDA #0
	BEQ GIVAYF
	
.ERRDIR
	LDX CURLIN+1
	INX
	BNE DIMRTS
	
	LDX #ERRID
	HOP2
	
.ERRGUF
	LDX #ERRUF
	JMP ERROR
	
.DEF
	JSR GETFNM
	JSR ERRDIR
	JSR CHKOPN
	LDA #128
	STA SUBFLG
	JSR PTRGET
	JSR CHKNUM
	JSR CHKCLS
	LDA #$B2
	JSR SYNCHR
	PHA
	LDA VARPNT+1
	PHA
	LDA VARPNT
	PHA
	LDA TXTPTR+1
	PHA
	LDA TXTPTR
	PHA
	JSR DATA
	JMP DEFFIN
	
.GETFNM
	LDA #FNTK
	JSR SYNCHR
	ORA #128
	STA SUBFLG
	JSR PTRGT2
	STA DEFPNT
	STY DEFPNT+1
	JMP CHKNUM
	
.FNDOER
	JSR GETFNM
	LDA DEFPNT+1
	PHA
	LDA DEFPNT
	PHA
	JSR PARCHK
	JSR CHKNUM
	PLA
	STA DEFPNT
	PLA
	STA DEFPNT+1
	LDY #2
	LDA (DEFPNT),Y
	STA VARPNT
	TAX
	INY
	LDA (DEFPNT),Y
	BEQ ERRGUF
	
	STA VARPNT+1
	INY
	
.DEFSTF
	LDA (VARPNT),Y
	PHA
	DEY
	BPL DEFSTF
	
	LDY VARPNT+1
	JSR MOVMF
	LDA TXTPTR+1
	PHA
	LDA TXTPTR
	PHA
	LDA (DEFPNT),Y
	STA TXTPTR
	INY
	LDA (DEFPNT),Y
	STA TXTPTR+1
	LDA VARPNT+1
	PHA
	LDA VARPNT
	PHA
	JSR FRMNUM
	PLA
	STA DEFPNT
	PLA
	STA DEFPNT+1
	JSR CHRGOT
	BEQ *+5
	
	JMP SNERR
	
	PLA
	STA TXTPTR
	PLA
	STA TXTPTR+1
	
.DEFFIN
	LDY #0
	PLA
	STA (DEFPNT),Y
	PLA
	INY
	STA (DEFPNT),Y
	PLA
	INY
	STA (DEFPNT),Y
	PLA
	INY
	STA (DEFPNT),Y
	PLA
	INY
	STA (DEFPNT),Y
	RTS
	
.STRD
	JSR CHKNUM
	LDY #0
	JSR FOUTC
	PLA
	PLA
	
.TIMSTR
	LDA #<LOFBUF
	LDY #>LOFBUF
IF BEEB
	BNE STRLIT
ELSE
	BEQ STRLIT
ENDIF
	
.STRINI
	LDX FACMO
	LDY FACMO+1
	STX DSCPNT
	STY DSCPNT+1
	
.STRSPA
	JSR GETSPA
	STX DSCTMP+1
	STY DSCTMP+2
	STA DSCTMP
	RTS
	
.STRLIT
	LDX #34
	STX CHARAC
	STX ENDCHR
	
.STRLT2
	STA STRNG1
	STY STRNG1+1
	STA DSCTMP+1
	STY DSCTMP+2
	LDY #255
	
.STRGET
	INY
	LDA (STRNG1),Y
	BEQ STRFI1
	
	CMP CHARAC
	BEQ STRFIN
	
	CMP ENDCHR
	BNE STRGET
	
.STRFIN
	CMP #34
	BEQ STRFI2
	
.STRFI1
	CLC

.STRFI2
	STY DSCTMP
	TYA
	ADC STRNG1
	STA STRNG2
	LDX STRNG1+1
	BCC STRST2
	
	INX
	
.STRST2
	STX STRNG2+1
	LDA STRNG1+1
	BEQ STRCP
	
	CMP #BUFPAG
	BNE PUTNEW
	
.STRCP
	TYA
	JSR STRINI
	LDX STRNG1
	LDY STRNG1+1
	JSR MOVSTR
	
.PUTNEW
	LDX TEMPPT
	CPX #TEMPST+STRSIZ+STRSIZ+STRSIZ
	BNE	PUTNW1
	LDX #ERRST
	
.ERRGO2
	JMP ERROR

.PUTNW1
	LDA DSCTMP
	STA 0,X
	LDA DSCTMP+1
	STA 1,X
	LDA DSCTMP+2
	STA 2,X
	LDY #0
	STX FACMO
	STY FACMO+1
	STY FACOV
	DEY
	STY VALTYP
	STX LASTPT
	INX
	INX
	INX
	STX TEMPPT
	RTS
	
.GETSPA
	LSR GARBFL
	
.TRYAG2
	PHA
	EOR #255
	SEC
	ADC FRETOP
	LDY FRETOP+1
	BCS TRYAG3
	
	DEY
	
.TRYAG3
	CPY STREND+1
	BCC GARBAG
	BNE STRFRE
	
	CMP STREND
	BCC GARBAG
	
.STRFRE
	STA FRETOP
	STY FRETOP+1
	STA FRESPC
	STY FRESPC+1
	TAX
	PLA
	RTS
	
.GARBAG
	LDX #ERROM      ;OUT OF MEMORY
	LDA GARBFL
	BMI ERRGO2
	
	JSR GARBA2
	LDA #128
	STA GARBFL
	PLA
	BNE TRYAG2
	
.GARBA2
	LDX MEMSIZ
	LDA MEMSIZ+1
	
.FNDVAR
	STX FRETOP
	STA FRETOP+1
	LDY #0
	STY GRBPNT+1
	STY GRBPNT
	LDA STREND
	LDX STREND+1
	STA GRBTOP
	STX GRBTOP+1
	LDA #<TEMPST
	LDX #>TEMPST
	STA INDEX1
	STX INDEX1+1
	
.TVAR
	CMP TEMPPT
	BEQ SVARS
	JSR DVAR
	BEQ TVAR
	
.SVARS
	LDA #6+ADDPRC
	STA FOUR6
	LDA VARTAB
	LDX VARTAB+1
	STA INDEX1
	STX INDEX1+1
	
.SVAR
	CPX ARYTAB+1
	BNE SVARGO
	
	CMP ARYTAB
	BEQ ARYVAR
	
.SVARGO
	JSR DVARS
	BEQ SVAR
	
.ARYVAR
	STA ARYPNT
	STX ARYPNT+1
	LDA #STRSIZ
	STA FOUR6
	
.ARYVA2
	LDA ARYPNT
	LDX ARYPNT+1
	
.ARYVA3
	CPX STREND+1
	BNE ARYVGO
	
	CMP STREND
	BNE *+5
	
	JMP GRBPAS
	
.ARYVGO
	STA INDEX1
	STX INDEX1+1
	LDY #1-ADDPRC
	LDA (INDEX1),Y
	TAX
	INY
	LDA (INDEX1),Y
	PHP
	INY
	LDA (INDEX1),Y
	ADC ARYPNT
	STA ARYPNT
	INY
	LDA (INDEX1),Y
	ADC ARYPNT+1
	STA ARYPNT+1
	PLP
	BPL ARYVA2
	
	TXA
	BMI ARYVA2
	
	INY
	LDA (INDEX1),Y
	LDY #0
	ASL A
	ADC #5
	ADC INDEX1
	STA INDEX1
	BCC ARYGET
	
	INC INDEX1+1
	
.ARYGET
	LDX INDEX1+1
	
.ARYSTR
	CPX ARYPNT+1
	BNE GOGO
	
	CMP ARYPNT
	BEQ ARYVA3
	
.GOGO
	JSR DVAR
	BEQ ARYSTR
	
.DVARS
	LDA (INDEX1),Y
	BMI DVARTS
	
	INY
	LDA (INDEX1),Y
	BPL DVARTS
	
	INY
	
.DVAR
	LDA (INDEX1),Y
	BEQ DVARTS
	
	INY
	LDA (INDEX1),Y
	TAX
	INY
	LDA (INDEX1),Y
	CMP FRETOP+1
	BCC DVAR2
	BNE DVARTS
	
	CPX FRETOP
	BCS DVARTS
	
.DVAR2
	CMP GRBTOP+1
	BCC DVARTS
	BNE DVAR3
	
	CPX GRBTOP
	BCC DVARTS
	
.DVAR3
	STX GRBTOP
	STA GRBTOP+1
	LDA INDEX1
	LDX INDEX1+1
	STA GRBPNT
	STX GRBPNT+1
	LDA FOUR6
	STA SIZE
	
.DVARTS
	LDA FOUR6
	CLC
	ADC INDEX1
	STA INDEX1
	BCC GRBRTS
	
	INC INDEX1+1
	
.GRBRTS
	LDX INDEX1+1
	LDY #0
	RTS
	
.GRBPAS
	LDA GRBPNT+1
	ORA GRBPNT
	BEQ GRBRTS
	
	LDA SIZE
	AND #4
	LSR A
	TAY
	STA SIZE
	LDA (GRBPNT),Y
	ADC LOWTR
	STA HIGHTR
	LDA LOWTR+1
	ADC #0
	STA HIGHTR+1
	LDA FRETOP
	LDX FRETOP+1
	STA HIGHDS
	STX HIGHDS+1
	JSR BLTUC
	LDY SIZE
	INY
	LDA HIGHDS
	STA (GRBPNT),Y
	TAX
	INC HIGHDS+1
	LDA HIGHDS+1
	INY
	STA (GRBPNT),Y
	JMP FNDVAR
	
.CAT
	LDA FACLO
	PHA
	LDA FACMO
	PHA
	JSR EVAL
	JSR CHKSTR
	PLA
	STA STRNG1
	PLA
	STA STRNG1+1
	LDY #0
	LDA (STRNG1),Y
	CLC
	ADC (FACMO),Y
	BCC SIZEOK
	
	LDX #ERRLS
	JMP ERROR
	
.SIZEOK
	JSR STRINI
	JSR MOVINS
	LDA DSCPNT
	LDY DSCPNT+1
	JSR FRETMP
	JSR MOVDO
	LDA STRNG1
	LDY STRNG1+1
	JSR FRETMP
	JSR PUTNEW
	JMP TSTOPX
	
.MOVINS
	LDY #0
	LDA (STRNG1),Y
	PHA
	INY
	LDA (STRNG1),Y
	TAX
	INY
	LDA (STRNG1),Y
	TAY
	PLA
	
.MOVSTR
	STX INDEX
	STY INDEX+1
	
.MOVDO
	TAY
	BEQ MVDONE
	
	PHA
	
.MOVLP
	DEY
	LDA (INDEX),Y
	STA (FRESPC),Y
	TYA
	BNE MOVLP
	
	PLA
	
.MVDONE
	CLC
	ADC FRESPC
	STA FRESPC
	BCC MVSTRT
	
	INC FRESPC+1
	
.MVSTRT
	RTS

.FRESTR
	JSR CHKSTR

.FREFAC
	LDA FACMO
	LDY FACMO+1
	
.FRETMP
	STA INDEX
	STY INDEX+1
	JSR FRETMS
	PHP
	LDY #0
	LDA (INDEX),Y
	PHA
	INY
	LDA (INDEX),Y
	TAX
	INY
	LDA (INDEX),Y
	TAY
	PLA
	PLP
	BNE FRETRT
	
	CPY FRETOP+1
	BNE FRETRT
	
	CPX FRETOP
	BNE FRETRT
	;
	;STRING WAS LAST INTO STRING SFACE
	;SAVE GARBAGE COLLECTION SOME TIME
	;BY FREEING UP. (LENGTH + 2)
	;
	PHA             ;SAVE LENGTH ON STACK
	CLC
	ADC FRETOP
	STA FRETOP
	BCC FREPLA
	
	INC FRETOP+1
	
.FREPLA
	PLA             ;PULL LENGTH OFF STACK
	
.FRETRT
	STX INDEX
	STY INDEX+1
	RTS
	
.FRETMS
	CPY LASTPT+1
	BNE FRERTS
	
	CMP LASTPT
	BNE FRERTS
	
	STA TEMPPT
	SBC #STRSIZ
	STA LASTPT
	LDY #0
	
.FRERTS
	RTS
	
.CHRD
	JSR CONINT
	TXA
	PHA
	LDA #1
	JSR STRSPA
	PLA
	LDY #0
	STA (DSCTMP+1),Y
	PLA
	PLA
	JMP PUTNEW
	
.LEFTD
	JSR PREAM
	CMP (DSCPNT),Y
	TYA
	
.RLEFT
	BCC RLEFT1
	LDA (DSCPNT),Y
	TAX
	TYA
	
.RLEFT1
	PHA
	
.RLEFT2
	TXA
	
.RLEFT3
	PHA
	JSR STRSPA
	LDA DSCPNT
	LDY DSCPNT+1
	JSR FRETMP
	PLA
	TAY
	PLA
	CLC
	ADC INDEX
	STA INDEX
	BCC PULMOR
	
	INC INDEX+1
	
.PULMOR
	TYA
	JSR MOVDO
	JMP PUTNEW
	
.RIGHTD	
	JSR PREAM
	CLC
	SBC (DSCPNT),Y
	EOR #255
	JMP RLEFT
	
.MIDD
	LDA #255
	STA FACLO
	JSR CHRGOT
	CMP #41
	BEQ MID2
	
	JSR CHKCOM
	JSR GETBYT
	
.MID2
	JSR PREAM
	BEQ GOFUC
	
	DEX
	TXA
	PHA
	CLC
	LDX #0
	SBC (DSCPNT),Y
	BCS RLEFT2
	
	EOR #255
	CMP FACLO
	BCC RLEFT3
	
	LDA FACLO
	BCS RLEFT3
	
.PREAM
	JSR CHKCLS
	PLA
	TAY
	PLA
	STA JMPER+1
	PLA
	PLA
	PLA
	TAX
	PLA
	STA DSCPNT
	PLA
	STA DSCPNT+1
	LDA JMPER+1
	PHA
	TYA
	PHA
	LDY #0
	TXA
	RTS
	
.LEN
	JSR LEN1
	JMP SNGFLT
	
.LEN1
	JSR FRESTR
	LDX #0
	STX VALTYP
	TAY
	RTS
	
.ASC
	JSR LEN1
	BEQ GOFUC
	LDY #0
	LDA (INDEX1),Y
	TAY
	JMP SNGFLT
	
.GOFUC
	JMP FCERR
	
.GTBYTC
	JSR CHRGET

.GETBYT
	JSR FRMNUM
	
.CONINT
	JSR POSINT
	LDX FACMO
	BNE GOFUC
	
	LDX FACLO
	JMP CHRGOT
	
.VAL
	JSR LEN1
	BNE *+5
	
	JMP ZEROFC
	
	LDX TXTPTR
	LDY TXTPTR+1
	STX STRNG2
	STY STRNG2+1
	LDX INDEX1
	STX TXTPTR
	CLC
	ADC INDEX1
	STA INDEX2
	LDX INDEX1+1
	STX TXTPTR+1
	BCC VAL2
	
	INX 
	
.VAL2
	STX INDEX2+1
	LDY #0
	LDA (INDEX2),Y
	PHA
	LDA #0
	STA (INDEX2),Y
	JSR CHRGOT
	JSR FIN
	PLA
	LDY #0
	STA (INDEX2),Y
	
.ST2TXT
	LDX STRNG2
	LDY STRNG2+1
	STX TXTPTR
	STY TXTPTR+1
	
.VALRTS
	RTS

.GETNUM
	JSR FRMNUM
	JSR GETADR
	
.COMBYT
	JSR CHKCOM
	JMP GETBYT
	
.GETADR
	LDA FACSGN
	BMI GOFUC
	
	LDA FACEXP
	CMP #145
	BCS GOFUC
	
	JSR QINT
	
	LDA FACMO
	LDY FACMO+1
	STY POKER
	STA POKER+1
	RTS
	
.PEEK
	LDA POKER+1
	PHA
	LDA POKER
	PHA
	JSR GETADR
	LDY #0

IF BEEB
	BIT POKER+1
	BMI PEKJMP
ENDIF

IF NOT(BEEB)
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
ENDIF
	
.GETCON
	LDA (POKER),Y
	TAY
	
.DOSGFL	
	PLA
	STA POKER
	PLA
	STA POKER+1
	JMP SNGFLT
	
IF BEEB
.PEKJMP
	JMP KPEEK
ENDIF
	
.POKE
	JSR GETNUM

IF BEEB
	BIT POKER+1
	BMI POKJMP
	
.POKK
ENDIF
	
	TXA
	
	LDY #0
	STA (POKER),Y
	RTS
	
IF BEEB
.POKJMP
	JMP KPOKE
ENDIF
	
.FNWAIT
	JSR GETNUM
	STX ANDMSK
	
	LDX #0
	JSR CHRGOT
	BEQ STORD1
	
	JSR COMBYT
	
.STORDO
	STX EORMSK
	LDY #0
	
.WAITER
	LDA (POKER),Y
	EOR EORMSK
	AND ANDMSK
	BEQ WAITER
	
.ZERRTS
	RTS

.FADDH
	LDA #<FHALF
	LDY #>FHALF
	JMP FADD
	
.FSUB
	JSR CONUPK

.FSUBT
	LDA FACSGN
	EOR #$FF
	STA FACSGN
	EOR ARGSGN
	STA ARISGN
	LDA FACEXP
	JMP FADDT
	
.STORD1
	LDA LINNUM
	CMP #$66
	BNE STORDO
	
	LDA LINNUM+1
	SBC #$19
	BNE STORDO

IF BEEB
	LDY #LO(SRAM)
	STY LINNUM
	TAY
	LDA #HI(SRAM)
	STA LINNUM+1
ELSE
	STA LINNUM
	TAY
	LDA #$80
	STA LINNUM+1
ENDIF
	
.MRCHKR
	LDX #10

.MRCHR
	LDA SINCON+30,X
	AND #$3F
	STA (LINNUM),Y
	INY
	BNE PKINC
	
	INC LINNUM+1
	
.PKINC
	DEX
	BNE MRCHR
	
	DEC LSTPNT
	BNE MRCHKR
	
	RTS
	
.FADD5
	JSR SHIFTR
	BCC FADD4
	
.FADD
	JSR CONUPK

.FADDT
	BNE *+5
	JMP MOVFA
	LDX FACOV
	STX OLDOV
	LDX #ARGEXP
	LDA ARGEXP
	
.FADDC
	TAY
	BEQ ZERRTS
	
	SEC
	SBC FACEXP
	BEQ FADD4
	BCC FADDA
	
	STY FACEXP
	LDY ARGSGN
	STY FACSGN
	EOR #$FF
	ADC #0
	LDY #0
	STY OLDOV
	LDX #FAC
	BNE FADD1
	
.FADDA
	LDY #0
	STY FACOV
	
.FADD1
	CMP #$F9
	BMI FADD5
	
	TAY
	LDA FACOV
	LSR 1,X
	JSR ROLSHF
	
.FADD4
	BIT ARISGN
	BPL FADD2
	
	LDY #FACEXP
	CPX #ARGEXP
	BEQ SUBIT
	
	LDY #ARGEXP
	
.SUBIT
	SEC
	EOR #$FF
	ADC OLDOV
	STA FACOV
	LDA 3+ADDPRC,Y
	SBC 3+ADDPRC,X
	STA FACLO
	LDA ADDPRC+2,Y
	SBC 2+ADDPRC,X
	STA FACMO
	LDA 2,Y
	SBC 2,X
	STA FACMOH
	LDA 1,Y
	SBC 1,X
	STA FACHO
	
.FADFLT
	BCS NORMAL
	JSR NEGFAC
	
.NORMAL
	LDY #0
	TYA
	CLC
	
.NORM3
	LDX FACHO
	BNE NORM1
	LDX FACHO+1
	STX FACHO
	LDX FACMOH+1
	STX FACMOH
	LDX FACMO+1
	STX FACMO
	LDX FACOV
	STX FACLO
	STY FACOV
	ADC #$8
	
ADDPR2	=ADDPRC+ADDPRC
ADDPR4	=ADDPR2+ADDPR2
ADDPR8	=ADDPR4+ADDPR4

	CMP #$18+ADDPR8
	BNE NORM3
	
.ZEROFC
	LDA #0

.ZEROF1
	STA FACEXP

.ZEROML
	STA FACSGN
	RTS
	
.FADD2
	ADC OLDOV
	STA FACOV
	LDA FACLO
	ADC ARGLO
	STA FACLO
	LDA FACMO
	ADC ARGMO
	STA FACMO
	LDA FACMOH
	ADC ARGMOH
	STA FACMOH
	LDA FACHO
	ADC ARGHO
	STA FACHO
	JMP SQUEEZ
	
.NORM2
	ADC #1
	ASL FACOV
	ROL FACLO
	ROL FACMO
	ROL FACMOH
	ROL FACHO
	
.NORM1
	BPL NORM2
	
	SEC
	SBC FACEXP
	BCS ZEROFC
	
	EOR #$FF
	ADC #1
	STA FACEXP
	
.SQUEEZ
	BCC RNDRTS

.RNDSHF
	INC FACEXP
	BEQ OVERR
	
	ROR FACHO
	ROR FACMOH
	ROR FACMO
	ROR FACLO
	ROR FACOV
	
.RNDRTS
	RTS

.NEGFAC
	LDA FACSGN
	EOR #$FF
	STA FACSGN
	
.NEGFCH
	LDA FACHO
	EOR #$FF
	STA FACHO
	LDA FACMOH
	EOR #$FF
	STA FACMOH
	LDA FACMO
	EOR #$FF
	STA FACMO
	LDA FACLO
	EOR #$FF
	STA FACLO
	LDA FACOV
	EOR #$FF
	STA FACOV
	INC FACOV
	BNE INCFRT
	
.INCFAC
	INC FACLO
	BNE INCFRT
	
	INC FACMO
	BNE INCFRT
	
	INC FACMOH
	BNE INCFRT
	
	INC FACHO
	
.INCFRT
	RTS
	
.OVERR
	LDX #ERROV
	JMP ERROR
	
.MULSHF
	LDX #RESHO-1

.SHFTR2
	LDY 3+ADDPRC,X
	STY FACOV
	LDY 3,X
	STY 4,X
	LDY 2,X
	STY 3,X
	LDY 1,X
	STY 2,X
	LDY BITS
	STY 1,X
	
.SHIFTR
	ADC #$8
	BMI SHFTR2
	BEQ SHFTR2
	
	SBC #$8
	TAY
	LDA FACOV
	BCS SHFTRT
	
.SHFTR3
	ASL 1,X
	BCC SHFTR4
	
	INC 1,X
	
.SHFTR4
	ROR 1,X
	ROR 1,X
	
.ROLSHF
	ROR 2,X
	ROR 3,X
	ROR 4,X
	ROR A
	INY
	BNE SHFTR3
	
.SHFTRT
	CLC
	RTS
	
.FONE
	EQUB $81,0,0,0,0
	
.LOGCN2
	EQUB 3,$7F,$5E,$56
	EQUB $CB,$79,$80,$13
	EQUB $9B,$B,$64,$80
	EQUB $76,$38,$93,$16
	EQUB $82,$38,$AA,$3B,$20
	
.SQR05
	EQUB $80,$35,4,$F3,$34
	
.SQR20
	EQUB $81,$35,$4,$F3,$34
	
.NEGHLF
	EQUB $80,$80,0,0,0
	
.LOG2
	EQUB $80,$31,$72,$17,$F8

.LOG
	JSR SIGN
	BEQ LOGERR
	BPL LOG1
	
.LOGERR
	JMP FCERR
	
.LOG1
	LDA FACEXP
	SBC #$7F
	PHA
	LDA #$80
	STA FACEXP
	LDA #<SQR05
	LDY #>SQR05
	JSR FADD
	LDA #<SQR20
	LDY #>SQR20
	JSR FDIV
	LDA #<FONE
	LDY #>FONE
	JSR FSUB
	LDA #<LOGCN2
	LDY #>LOGCN2
	JSR POLYX
	LDA #<NEGHLF
	LDY #>NEGHLF
	JSR FADD
	PLA
	JSR FINLOG
	LDA #<LOG2
	LDY #>LOG2
	
.FMULT
	JSR CONUPK
	
.FMULTT
	BNE *+5
	JMP MULTRT
	
	JSR MULDIV
	LDA #0
	STA RESHO
	STA RESMOH
	STA RESMO
	STA RESLO
	LDA FACOV
	JSR MLTPLY
	LDA FACLO
	JSR MLTPLY
	LDA FACMO
	JSR MLTPLY
	LDA FACMOH
	JSR MLTPLY
	LDA FACHO
	JSR MLTPL1
	JMP MOVFR
	
.MLTPLY
	BNE *+5

	JMP MULSHF
	
.MLTPL1
	LSR A
	ORA #$80
	
.MLTPL2
	TAY
	BCC MLTPL3
	
	CLC
	LDA RESLO
	ADC ARGLO
	STA RESLO
	LDA RESMO
	ADC ARGMO
	STA RESMO
	LDA RESMOH
	ADC ARGMOH
	STA RESMOH
	LDA RESHO
	ADC ARGHO
	STA RESHO
	
.MLTPL3
	ROR RESHO
	ROR RESMOH
	ROR RESMO
	ROR RESLO
	ROR FACOV
	TYA
	LSR A
	BNE MLTPL2
	
.MULTRT
	RTS
	
.CONUPK
	STA INDEX1
	STY INDEX1+1
	LDY #4
	LDA (INDEX1),Y
	STA ARGLO
	DEY
	LDA (INDEX),Y
	STA ARGMO
	DEY
	LDA (INDEX1),Y
	STA ARGMOH
	DEY
	LDA (INDEX1),Y
	STA ARGSGN
	EOR FACSGN
	STA ARISGN
	LDA ARGSGN
	ORA #$80
	STA ARGHO
	DEY
	LDA (INDEX1),Y
	STA ARGEXP
	LDA FACEXP
	RTS
	
.MULDIV
	LDA ARGEXP
	
.MLDEXP
	BEQ ZEREMV
	CLC
	ADC FACEXP
	BCC TRYOFF
	BMI GOOVER
	
	CLC
	HOP2
	
.TRYOFF
	BPL ZEREMV
	
	ADC #$80
	STA FACEXP
	BNE *+5
	
	JMP ZEROML
	
	LDA ARISGN
	STA FACSGN
	RTS
	
.MLDVEX
	LDA FACSGN
	EOR #$FF
	BMI GOOVER
	
.ZEREMV
	PLA
	PLA
	JMP ZEROFC
	
.GOOVER
	JMP OVERR
	
.MUL10
	JSR MOVAF
	TAX
	BEQ MUL10R
	
	CLC
	ADC #2
	BCS GOOVER
	
.FINML6
	LDX #0
	STX ARISGN
	JSR FADDC
	INC FACEXP
	BEQ GOOVER
	
.MUL10R	RTS

.TENC
	EQUB $84,$20,0,0,0
	
.DIV10
	JSR MOVAF
	LDA #<TENC
	LDY #>TENC
	LDX #0
	
.FDIVF
	STX ARISGN
	JSR MOVFM
	JMP FDIVT
	
.FDIV
	JSR CONUPK
	
.FDIVT
	BEQ DV0ERR
	
	JSR ROUND
	LDA #0
	SEC
	SBC FACEXP
	STA FACEXP
	JSR MULDIV
	INC FACEXP
	BEQ GOOVER
	
	LDX #$FC
	LDA #1
	
.DIVIDE
	LDY ARGHO
	CPY FACHO
	BNE SAVQUO
	
	LDY ARGMOH
	CPY FACMOH
	BNE SAVQUO
	
	LDY ARGMO
	CPY FACMO
	BNE SAVQUO
	
	LDY ARGLO
	CPY FACLO
	
.SAVQUO
	PHP
	ROL A
	BCC QSHFT
	
	INX
	STA RESLO,X
	BEQ LD100
	BPL DIVNRM
	
	LDA #1
	
.QSHFT
	PLP
	BCS DIVSUB
	
.SHFARG
	ASL ARGLO
	ROL ARGMO
	ROL ARGMOH
	ROL ARGHO
	BCS SAVQUO
	BMI DIVIDE
	BPL SAVQUO
	
.DIVSUB
	TAY
	LDA ARGLO
	SBC FACLO
	STA ARGLO
	LDA ARGMO
	SBC FACMO
	STA ARGMO
	LDA ARGMOH
	SBC FACMOH
	STA ARGMOH
	LDA ARGHO
	SBC FACHO
	STA ARGHO
	TYA
	JMP SHFARG
	
.LD100
	LDA #$40
	BNE QSHFT
	
.DIVNRM
	ASL A
	ASL A
	ASL A
	ASL A
	ASL A
	ASL A
	STA FACOV
	PLP
	JMP MOVFR
	
.DV0ERR
	LDX #ERRDVO
	JMP ERROR

.MOVFR
	LDA RESHO
	STA FACHO
	LDA RESMOH
	STA FACMOH
	LDA RESMO
	STA FACMO
	LDA RESLO
	STA FACLO
	JMP NORMAL
	
.MOVFM
	STA INDEX1
	STY INDEX1+1
	LDY #3+ADDPRC
	LDA (INDEX1),Y
	STA FACLO
	DEY
	LDA (INDEX1),Y
	STA FACMO
	DEY
	LDA (INDEX1),Y
	STA FACMOH
	DEY
	LDA (INDEX1),Y
	STA FACSGN
	ORA #$80
	STA FACHO
	DEY
	LDA (INDEX1),Y
	STA FACEXP
	STY FACOV
	RTS
	
.MOV2F
	LDX #TEMPF2
	HOP2
	
.MOV1F
	LDX #TEMPF1
	LDY #0
	BEQ MOVMF
	
.MOVVF
	LDX FORPNT
	LDY FORPNT+1
	
.MOVMF
	JSR ROUND
	STX INDEX1
	STY INDEX1+1
	LDY #3+ADDPRC
	LDA FACLO
	STA (INDEX),Y
	DEY
	LDA FACMO
	STA (INDEX),Y
	DEY
	LDA FACMOH
	STA (INDEX),Y
	DEY
	LDA FACSGN
	ORA #$7F
	AND FACHO
	STA (INDEX),Y
	DEY
	LDA FACEXP
	STA (INDEX),Y
	STY FACOV
	RTS
	
.MOVFA
	LDA ARGSGN
	
.MOVFA1
	STA FACSGN
	LDX #4+ADDPRC
	
.MOVFAL
	LDA ARGEXP-1,X
	STA FACEXP-1,X
	DEX
	BNE MOVFAL
	
	STX FACOV
	RTS
	
.MOVAF
	JSR ROUND
	
.MOVEF
	LDX #5+ADDPRC
	
.MOVAFL
	LDA FACEXP-1,X
	STA ARGEXP-1,X
	DEX
	BNE MOVAFL
	
	STX FACOV
	
.MOVRTS
	RTS
	
.ROUND
	LDA FACEXP
	BEQ MOVRTS
	
	ASL FACOV
	BCC MOVRTS
	
.INCRND
	JSR INCFAC
	BNE MOVRTS
	
	JMP RNDSHF
	
.SIGN
	LDA FACEXP
	BEQ SIGNRT
	
.FCSIGN
	LDA FACSGN
	
.FCOMPS
	ROL A
	LDA #$FF
	BCS SIGNRT
	
	LDA #1
	
.SIGNRT
	RTS
	
.SGN
	JSR SIGN
	
.FLOAT
	STA FACHO
	LDA #0
	STA FACHO+1
	LDX #$88
	
.FLOATS
	LDA FACHO
	EOR #$FF
	ROL A
	
.FLOATC
	LDA #0
	STA FACLO
	STA FACMO
	
.FLOATB
	STX FACEXP
	STA FACOV
	STA FACSGN
	JMP FADFLT
	
.ABS
	LSR FACSGN
	RTS
	
.FCOMP
	STA INDEX2
	
.FCOMPN
	STY INDEX2+1
	LDY #0
	LDA (INDEX2),Y
	INY
	TAX
	BEQ SIGN
	
	LDA (INDEX2),Y
	EOR FACSGN
	BMI FCSIGN
	
	CPX FACEXP
	BNE FCOMPC
	
	LDA (INDEX2),Y
	ORA #$80
	CMP FACHO
	BNE FCOMPC
	
	INY
	LDA (INDEX2),Y
	CMP FACMOH
	BNE FCOMPC
	
	INY
	LDA (INDEX2),Y
	CMP FACMO
	BNE FCOMPC
	
	INY
	LDA #$7F
	CMP FACOV
	LDA (INDEX2),Y
	SBC FACLO
	BEQ QINTRT
	
.FCOMPC
	LDA FACSGN
	BCC FCOMPD
	
	EOR #$FF
	
.FCOMPD
	JMP FCOMPS

.QINT
	LDA FACEXP
	BEQ CLRFAC
	
	SEC
	SBC #ADDPR8+$98
	BIT FACSGN
	BPL QISHFT
	
	TAX
	LDA #$FF
	STA BITS
	JSR NEGFCH
	TXA
	
.QISHFT
	LDX #FAC
	CMP #$F9
	BPL QINT1
	
	JSR SHIFTR
	STY BITS
	
.QINTRT
	RTS
	
.QINT1
	TAY
	LDA FACSGN
	AND #$80
	LSR FACHO
	ORA FACHO
	STA FACHO
	JSR ROLSHF
	STY BITS
	RTS
	
.INT
	LDA FACEXP
	CMP #ADDPR8+$98
	BCS INTRTS
	JSR QINT
	STY FACOV
	LDA FACSGN
	STY FACSGN
	EOR #$80
	ROL A
	LDA #$98+8
	STA FACEXP
	LDA FACLO
	STA INTEGR
	JMP FADFLT
	
.CLRFAC
	STA FACHO
	STA FACMOH
	STA FACMO
	STA FACLO
	TAY
	
.INTRTS
	RTS
	
.FIN
	LDY #$0
	LDX #$9+ADDPRC
	
.FINZLP
	STY DECCNT,X
	DEX
	BPL FINZLP
	BCC FINDGQ
	
	CMP #'-'
	BNE QPLUS
	
	STX SGNFLG
	BEQ FINC
	
.QPLUS
	CMP #'+'
	BNE FIN1
	
.FINC
	JSR CHRGET
	
.FINDGQ
	BCC FINDIG
	
.FIN1
	CMP #'.'
	BEQ FINDP
	
	CMP #'E'
	BNE FINE
	
	JSR CHRGET
	BCC FNEDG1
	CMP #MINUTK
	BEQ FINEC1
	
	CMP #'-'
	BEQ FINEC1
	
	CMP #PLUSTK
	BEQ FINEC
	
	CMP #'+'
	BEQ FINEC
	BNE FINEC2
	
.FINEC1
	ROR EXPSGN
	
.FINEC
	JSR CHRGET
	
.FNEDG1
	BCC FINEDG
	
.FINEC2
	BIT EXPSGN
	BPL FINE
	
	LDA #0
	SEC
	SBC TENEXP
	JMP FINE1
	
.FINDP
	ROR DPTFLG
	BIT DPTFLG
	BVC FINC
	
.FINE
	LDA TENEXP
	
.FINE1
	SEC
	SBC DECCNT
	STA TENEXP
	BEQ FINQNG
	BPL FINMUL
	
.FINDIV
	JSR DIV10
	INC TENEXP
	BNE FINDIV
	BEQ FINQNG
	
.FINMUL
	JSR MUL10
	DEC TENEXP
	BNE FINMUL
	
.FINQNG
	LDA SGNFLG
	BMI NEGXQS
	
	RTS
	
.NEGXQS
	JMP NEGOP
	
.FINDIG
	PHA
	BIT DPTFLG
	BPL FINDG1
	
	INC DECCNT
	
.FINDG1
	JSR MUL10
	PLA
	SEC
	SBC #'0'
	JSR FINLOG
	JMP FINC
	
.FINLOG
	PHA
	JSR MOVAF
	PLA
	JSR FLOAT
	LDA ARGSGN
	EOR FACSGN
	STA ARISGN
	LDX FACEXP
	JMP FADDT
	
.FINEDG
	LDA TENEXP
	CMP #$A
	BCC MLEX10
	
	LDA #$64
	BIT EXPSGN
	BMI MLEXMI
	
	JMP OVERR
	
.MLEX10
	ASL A
	ASL A
	CLC
	ADC TENEXP
	ASL A
	CLC
	LDY #0
	ADC (TXTPTR),Y
	SEC
	SBC #'0'
	
.MLEXMI
	STA TENEXP
	JMP FINEC

.N0999	EQUB $9B,$3E,$BC,$1F,$FD

.N9999	EQUB $9E,$6E,$6B,$27,$FD

.NMIL	EQUB $9E,$6E,$6B,$28,0

.INPRT
	LDA #<INTXT
	LDY #>INTXT
	JSR STROU2
	LDA CURLIN+1
	LDX CURLIN
	
.LINPRT
	STA FACHO
	STX FACHO+1
	LDX #$90
	SEC
	JSR FLOATC
	JSR FOUT
	
.STROU2
	JMP STROUT

.FOUT
	LDY #1

.FOUTC
	LDA #' '
	BIT FACSGN
	BPL FOUT1
	
	LDA #'-'
	
.FOUT1
	STA FBUFFR-1,Y
	STA FACSGN
	STY FBUFPT
	INY
	LDA #'0'
	LDX FACEXP
	BNE *+5
	
	JMP FOUT19
	
	LDA #0
	CPX #$80
	BEQ FOUT37
	BCS FOUT7
	
.FOUT37
	LDA #<NMIL
	LDY #>NMIL
	JSR FMULT
	LDA #250-ADDPR2-ADDPRC
	
.FOUT7
	STA DECCNT

.FOUT4
	LDA #<N9999
	LDY #>N9999
	JSR FCOMP
	BEQ BIGGES
	BPL FOUT9
	
.FOUT3
	LDA #<N0999
	LDY #>N0999
	JSR FCOMP
	BEQ FOUT38
	BPL FOUT5
	
.FOUT38
	JSR MUL10
	DEC DECCNT
	BNE FOUT3
	
.FOUT9
	JSR DIV10
	INC DECCNT
	BNE FOUT4
	
.FOUT5
	JSR FADDH

.BIGGES
	JSR QINT
	LDX #1
	LDA DECCNT
	CLC
	ADC #ADDPR2+ADDPRC+7
	BMI FOUTPI
	
	CMP #ADDPR2+ADDPRC+$8
	BCS FOUT6
	
	ADC #$FF
	TAX
	LDA #2
	
.FOUTPI
	SEC
	
.FOUT6
	SBC #2
	STA TENEXP
	STX DECCNT
	TXA
	BEQ FOUT39
	BPL FOUT8
	
.FOUT39
	LDY FBUFPT
	LDA #'.'
	INY
	STA FBUFFR-1,Y
	TXA
	BEQ FOUT16
	
	LDA #'0'
	INY
	STA FBUFFR-1,Y
	
.FOUT16
	STY FBUFPT

.FOUT8
	LDY #0

.FOUTIM
	LDX #$80

.FOUT2
	LDA FACLO
	CLC
	ADC FOUTBL+2+ADDPRC,Y
	STA FACLO
	LDA FACMO
	ADC FOUTBL+1+ADDPRC,Y
	STA FACMO
	LDA FACMOH
	ADC FOUTBL+1,Y
	STA FACMOH
	LDA FACHO
	ADC FOUTBL,Y
	STA FACHO
	INX
	BCS FOUT41
	BPL FOUT2
	BMI FOUT40
	
.FOUT41
	BMI FOUT2

.FOUT40
	TXA
	BCC FOUTYP
	
	EOR #$FF
	ADC #$A
	
.FOUTYP
	ADC #$2F
	INY
	INY
	INY
	INY
	STY FDECPT
	LDY FBUFPT
	INY
	TAX
	AND #$7F
	STA FBUFFR-1,Y
	DEC DECCNT
	BNE STXBUF
	
	LDA #'.'
	INY
	STA FBUFFR-1,Y
	
.STXBUF
	STY FBUFPT
	LDY FDECPT
	TXA
	EOR #$FF
	AND #$80
	TAX
	CPY #FDCEND-FOUTBL
	BEQ FOULDY
	
	CPY #TIMEND-FOUTBL
	BNE FOUT2
	
.FOULDY
	LDY FBUFPT

.FOUT11
	LDA FBUFFR-1,Y
	DEY
	CMP #'0'
	BEQ FOUT11
	
	CMP #'.'
	BEQ FOUT12
	
	INY
	
.FOUT12
	LDA #'+'
	LDX TENEXP
	BEQ FOUT17
	BPL FOUT14
	LDA #0
	SEC
	SBC TENEXP
	TAX
	LDA #'-'
	
.FOUT14
	STA FBUFFR+1,Y
	LDA #'E'
	STA FBUFFR,Y
	TXA
	LDX #$2F
	SEC
	
.FOUT15
	INX
	SBC #$A
	BCS FOUT15
	ADC #$3A
	STA FBUFFR+3,Y
	TXA
	STA FBUFFR+2,Y
	LDA #0
	STA FBUFFR+4,Y
	BEQ FOUT20
	
.FOUT19
	STA FBUFFR-1,Y
	
.FOUT17
	LDA #0
	STA FBUFFR,Y
	
.FOUT20
	LDA #<FBUFFR
	LDY #>FBUFFR
	RTS
	
.FHALF
	EQUB $80,0
	
.ZERO
	EQUB 0,0,0

.FOUTBL
	EQUB $FA,$A,$1F,0,0
	EQUB $98,$96,$80,$FF
	EQUB $F0,$BD,$C0,0
	EQUB 1,$86,$A0,$FF
	EQUB $FF,$D8,$F0,0,0
	EQUB 3,$E8,$FF,$FF
	EQUB $FF,$9C,0,0,0,$A
	EQUB $FF,$FF,$FF,$FF
	
.FDCEND
	EQUB $FF,$DF,$A,$80
	EQUB 0,3,$4B,$C0,$FF
	EQUB $FF,$73,$60,0,0
	EQUB $E,$10,$FF,$FF
	EQUB $FD,$A8,0,0,0,$3C
.TIMEND

.SQR
	JSR MOVAF
	LDA #<FHALF
	LDY #>FHALF
	JSR MOVFM
	
.FPWRT
	BEQ EXP
	LDA ARGEXP
	BNE FPWRT1
	
	JMP ZEROF1
	
.FPWRT1
	LDX #<TEMPF3
	LDY #>TEMPF3
	JSR MOVMF
	LDA ARGSGN
	BPL FPWR1
	
	JSR INT
	LDA #<TEMPF3
	LDY #>TEMPF3
	JSR FCOMP
	BNE FPWR1
	
	TYA
	LDY INTEGR
	
.FPWR1
	JSR MOVFA1
	TYA
	PHA
	JSR LOG
	LDA #<TEMPF3
	LDY #>TEMPF3
	JSR FMULT
	JSR EXP
	PLA
	LSR A
	BCC NEGRTS
	
.NEGOP
	LDA FACEXP
	BEQ NEGRTS
	
	LDA FACSGN
	EOR #$FF
	STA FACSGN
	
.NEGRTS
	RTS

.LOGEB2
	EQUB $81,$38,$AA,$3B,$29

.EXPCON
	EQUB 7,$71,$34,$58,$3E
	EQUB $56,$74,$16,$7E
	EQUB $B3,$1B,$77,$2F
	EQUB $EE,$E3,$85,$7A
	EQUB $1D,$84,$1C,$2A
	EQUB $7C,$63,$59,$58
	EQUB $A,$7E,$75,$FD
	EQUB $E7,$C6,$80,$31
	EQUB $72,$18,$10,$81
	EQUB 0,0,0,0
	
.EXP
	LDA #<LOGEB2
	LDY #>LOGEB2
	JSR FMULT
	LDA FACOV
	ADC #$50
	BCC STOLD
	JSR INCRND


	;CODE25
	
.STOLD
	STA OLDOV
	JSR MOVEF
	LDA FACEXP
	CMP #$88
	BCC EXP1
	
.GOMLDV
	JSR MLDVEX
	
.EXP1
	JSR INT
	LDA INTEGR
	CLC
	ADC #$81
	BEQ GOMLDV
	SEC
	SBC #1
	PHA
	LDX #4+ADDPRC
	
.SWAPLP
	LDA ARGEXP,X
	LDY FACEXP,X
	STA FACEXP,X
	STY ARGEXP,X
	DEX
	BPL SWAPLP
	
	LDA OLDOV
	STA FACOV
	JSR FSUBT
	JSR NEGOP
	LDA #<EXPCON
	LDY #>EXPCON
	JSR POLY
	LDA #0
	STA ARISGN
	PLA
	JSR MLDEXP
	RTS
	
.POLYX
	STA POLYPT
	STY POLYPT+1
	JSR MOV1F
	LDA #TEMPF1
	JSR FMULT
	JSR POLY1
	LDA #<TEMPF1
	LDY #>TEMPF1
	JMP FMULT
	
.POLY
	STA POLYPT
	STY POLYPT+1
	
.POLY1
	JSR MOV2F
	LDA (POLYPT),Y
	STA DEGREE
	LDY POLYPT
	INY
	TYA
	BNE POLY3
	
	INC POLYPT+1
	
.POLY3
	STA POLYPT
	LDY POLYPT+1
	
.POLY2
	JSR FMULT
	LDA POLYPT
	LDY POLYPT+1
	CLC
	ADC #4+ADDPRC
	BCC POLY4
	
	INY
	
.POLY4
	STA POLYPT
	STY POLYPT+1
	JSR FADD
	LDA #<TEMPF2
	LDY #>TEMPF2
	DEC DEGREE
	BNE POLY2
	
	RTS
	
.RMULC	EQUB $98,$35,$44,$7A

.RADDC	EQUB $68,$28,$B1,$46

.RND
	JSR SIGN
	BMI RND1
	BNE QSETNR

	LDA CHTIM
	STA FACHO
	LDA CHTIM+4
	STA FACMOH
	LDA CHTIM+1
	STA FACMO
	LDA CHTIM+5
	STA FACLO
	JMP STRNEX
	
.QSETNR
	LDA #<RNDX
	LDY #>RNDX
	JSR MOVFM
	LDA #<RMULC
	LDY #>RMULC
	JSR FMULT
	LDA #<RADDC
	LDY #>RADDC
	JSR FADD
	
.RND1
	LDX FACLO
	LDA FACHO
	STA FACLO
	STX FACHO
	LDX FACMOH
	LDA FACMO
	STA FACMOH
	STX FACMO
	
.STRNEX
	LDA #0
	STA FACSGN
	LDA FACEXP
	STA FACOV
	LDA #$80
	STA FACEXP
	JSR NORMAL
	LDX #<RNDX
	LDY #>RNDX
	
.GMOVMF
	JMP MOVMF

.COS
	LDA #<PI2
	LDY #>PI2
	JSR FADD
	
.SIN
	JSR MOVAF
	LDA #<TWOPI
	LDY #>TWOPI
	LDX ARGSGN
	JSR FDIVF
	JSR MOVAF
	JSR INT
	LDA #0
	STA ARISGN
	JSR FSUBT
	LDA #<FR4
	LDY #>FR4
	JSR FSUB
	LDA FACSGN
	PHA
	BPL SIN1
	
	JSR FADDH
	LDA FACSGN
	BMI SIN2
	
	LDA TANSGN
	EOR #$FF
	STA TANSGN
	
.SIN1
	JSR NEGOP

.SIN2
	LDA #<FR4
	LDY #>FR4
	JSR FADD
	PLA
	BPL SIN3
	
	JSR NEGOP
	
.SIN3
	LDA #<SINCON
	LDY #>SINCON
	JMP POLYX
	
.TAN
	JSR MOV1F
	LDA #0
	STA TANSGN
	JSR SIN
	LDX #<TEMPF3
	LDY #>TEMPF3
	JSR GMOVMF
	LDA #<TEMPF1
	LDY #>TEMPF1
	JSR MOVFM
	LDA #0
	STA FACSGN
	LDA TANSGN
	JSR COSC
	LDA #<TEMPF3
	LDY #>TEMPF3
	JMP FDIV
	
.COSC
	PHA
	JMP SIN1
	
.PI2
	EQUB $81,$49,$F,$DA,$A2
	
.TWOPI
	EQUB $83,$49,$F,$DA,$A2
	
.FR4
	EQUB $7F,0,0,0,0

.SINCON
	EQUB 5,$84,$E6,$1A,$2D
	EQUB $1B,$86,$28,$7,$FB
	EQUB $F8,$87,$99,$68,$89
	EQUB 1,$87,$23,$35,$DF,$E1
	EQUB $86,$A5,$5D,$E7,$28,$83
	EQUB $49,$F,$DA,$A2
	
.LE082
	EQUB $A1,$54,$46,$8F,$13,$8F,$52,$43
	EQUB $89,$CD
	
.ATN
	LDA FACSGN
	PHA
	BPL ATN1
	
	JSR NEGOP
	
.ATN1
	LDA FACEXP
	PHA
	CMP #$81
	BCC ATN2
	
	LDA #<FONE
	LDY #>FONE
	JSR FDIV
	
.ATN2
	LDA #<ATNCON
	LDY #>ATNCON
	JSR POLYX
	PLA
	CMP #$81
	BCC ATN3
	
	LDA #<PI2
	LDY #>PI2
	JSR FSUB
	
.ATN3
	PLA
	BPL ATN4
	
	JMP NEGOP
	
.ATN4
	RTS

.ATNCON
	EQUB $B,$76,$B3,$83
	EQUB $BD,$D3,$79,$1E,$F4
	EQUB $A6,$F5,$7B,$83,$FC
	EQUB $B0,$10
	EQUB $7C,$C,$1F,$67,$CA
	EQUB $7C,$DE,$53,$CB,$C1
	EQUB $7D,$14,$64,$70,$4C
	EQUB $7D,$B7,$EA,$51,$7A
	EQUB $7D,$63,$30,$88,$7E
	EQUB $7E,$92,$44,$99,$3A
	EQUB $7E,$4C,$CC,$91,$C7
	EQUB $7F,$AA,$AA,$AA,$13
	EQUB $81,0,0,0,0


{
.INITAT
	INC CHRGET+7
	BNE CHDGOT
	
	INC CHRGET+8
	
.CHDGOT
	LDA 60000
	CMP #':'
	BCS CHDRTS
	
	CMP #' '
	BEQ INITAT
	
	SEC
	SBC #'0'
	SEC
	SBC #$D0
	
.CHDRTS
	RTS
	
	EQUB 128,79,199,82,88
	
.*INIT
	LDX #STKEND-256
	TXS
	
	LDA #76
	STA JMPER
	STA USRPOK
	LDA #<FCERR
	LDY #>FCERR
	STA USRPOK+1
	STY USRPOK+2
	
	LDA #40
	STA LINWID
	
	LDA #30
	STA NCMWFL
	
	;COPY CHRGET TO ZP
	LDX #RNDX+4-CHRGET	; RNDX+5?
	
.MOVCHG
	LDA INITAT-1,X
	STA CHRGET-1,X
	DEX
	BNE MOVCHG
	;X=0
	
	LDA #STRSIZ
	STA FOUR6
	
	TXA	;A=X=0
	STA BITS
	STA CHANNL
	STA LASTPT+1
	
	PHA	;WHY? A==0
	STA CNTWFL
	
	INX	;X=1
	STX BUF-3;?
	STX BUF-4;?
	
	LDX #TEMPST
	STX TEMPPT

IF BEEB
	LDA RAMBOTTOM
	LDY RAMBOTTOM+1

	STA TXTTAB
	STY TXTTAB+1
	
	LDA RAMTOP
	LDY RAMTOP+1
ELSE
	LDY #>RAMLOC
	STA TXTTAB
	STY TXTTAB+1

	STA LINNUM
	STY LINNUM+1
	
	TAY	;Y=0

	; RAM TEST
.LOOPMM
	INC LINNUM
	BNE LOOPM1
	
	INC LINNUM+1
	BMI USEDEC
	
.LOOPM1
	LDA #85
	STA (LINNUM),Y
	CMP (LINNUM),Y
	BNE USEDEC
	
	ASL A
	STA (LINNUM),Y
	CMP (LINNUM),Y
	BEQ LOOPMM
	
.USEDEC
	LDA LINNUM
	LDY LINNUM+1
	
.USEDEF
ENDIF

	STA MEMSIZ
	STY MEMSIZ+1
	
	STA FRETOP
	STY FRETOP+1

IF NOT(BEEB)	
	LDX #<RAMLOC
	LDY #>RAMLOC
	
	STX TXTTAB
	STY TXTTAB+1
ENDIF
	
	LDY #0
	TYA
	STA (TXTTAB),Y
	
	INC TXTTAB
	
	LDA TXTTAB
	LDY TXTTAB+1
	
	JSR REASON
	
	LDA #<FREMES
	LDY #>FREMES
	JSR STROUT
	
	LDA MEMSIZ
	SEC
	SBC TXTTAB
	TAX
	LDA MEMSIZ+1
	SBC TXTTAB+1
	JSR LINPRT
	
	LDA #<WORDS
	LDY #>WORDS
	JSR STROUT
	
	JSR SCRTCH
	
	JMP READY
	
.WORDS
	EQUS " BYTES FREE",13,0
	
.FREMES
	EQUS "### COMMODORE BASIC ###"
	EQUB 13,13,0
}	




