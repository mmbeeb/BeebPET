   10REM P15 22/08/23
   20:
   30*TV255,1
   40:
   50MODE 7
   60:
   70SA%=&60C0
   80SCR2%=SA%-1000
   90SCR%=SCR2%-1000
  100CHARS%=SCR%-1024
  110:
  115R%=&2D00
  120keyin=R%+3
  130typerr=R%+6
  140wrt=R%+9
  150:
  160PROCass(3)
  170OSCLI "SAVE C.DSPLY 0900 "+STR$~end
  180END
  190:
  200:
  210DEFFNw(A%)=!A%AND&FFFF
  220DEFFNlo(A%)=A%AND&FF
  230DEFFNhi(A%)=(A%AND&FF00)DIV256
  240:
  250DEFPROCass(V%)
  260brkv=&202:eventv=&220:insv=&22A
  270bpnt=&FD
  280sbot=SA%:stop=sbot+25*40*8
  290sptr=&FA:pcount=&FD:savex=&F5
  300cal=&F8:cah=cal+1
  310cblink=&F6:ccount=&F7
  320:
  330flg=&90
  340tim=&8D
  350:
  360CODE%=&900
  370LOCALP%,opt%:FORopt%=0TOV%STEPV%
  380P%=CODE%:[OPTopt%
  390JMP reset
  400JMP trap
  410JMP curson
  420JMP cursoff
  430:
  440.trap SEI
  450LDA#FNlo(break):STAbrkv:LDA#FNhi(break):STAbrkv+1
  460LDA#FNlo(vsync):STAeventv:LDA#FNhi(vsync):STAeventv+1
  470LDA#FNlo(keyb):STAinsv:LDA#FNhi(keyb):STAinsv+1
  480CLI:JSRinit:JSRreset:SEC:RORccount:LDA#0:STAgmode:RTS
  490:
  500:
  510.reset LDY#250:.r1
  520LDA SCR2%-1,Y:EOR#&FF:STA SCR%-1,Y
  530LDA SCR2%+250-1,Y:EOR#&FF:STA SCR%+250-1,Y
  540LDA SCR2%+500-1,Y:EOR#&FF:STA SCR%+500-1,Y
  550LDA SCR2%+750-1,Y:EOR#&FF:STA SCR%+750-1,Y
  560DEY:BNE r1:LSRflg:RTS
  570:
  580.init LDA#0:EQUB&2C
  590.clr LDA#32:LDY#250:.c1
  600STA SCR2%-1,Y:STA SCR2%+250-1,Y:STA SCR2%+500-1,Y:STA SCR2%+750-1,Y
  610DEY:BNE c1:RTS
  620:
  630.cpoll DECccount:BNEcpx
  640INCcblink:LDA#17:STAccount:JSRcflip:.cpx JMPvsy
  650:
  660.vsync CMP#4:BNE vsx:PHP:SEI:TXA:PHA:TYA:PHA:JSRudtim:BITflg:BPLscan:BITccount:BPLcpoll
  670.vsy PLA:TAY:PLA:TAX:LDA#4:PLP:.vsx RTS
  680:
  690.scan
  700LDA#FNhi(SCR2%)+3:STA l1+2
  710LDA#FNhi(SCR%)+3:STA l2+2:STA l3+2
  720LDA#FNlo(stop):STA sptr
  730LDA#FNhi(stop):STA sptr+1
  740LDA#4:STApcount:LDX#FNlo(1000)
  750:
  760.l0 DEX
  770.k3 SEC:LDA sptr:SBC#8:STA sptr
  780BCC k1:.k2
  790.l1 LDA SCR2%,X
  800.l2 CMP SCR%,X:BNE l3
  810.l4 TXA:BNE l0
  820DEC pcount:BEQl5
  830DEC l1+2:DEC l2+2:DEC l3+2
  840BNE l0
  850:
  860.l5 SEC:RORflg:BMIvsy
  870.k1 DEC sptr+1:BNE k2
  880:
  890.l3 STA SCR%,X:STX savex
  900LDY#0:TAX:BMI v1
  910:
  920LDA CHARS%,X:STA (sptr),Y:INY
  930LDA CHARS%+128,X:STA (sptr),Y:INY
  940LDA CHARS%+2*128,X:STA (sptr),Y:INY
  950LDA CHARS%+3*128,X:STA (sptr),Y:INY
  960LDA CHARS%+4*128,X:STA (sptr),Y:INY
  970LDA CHARS%+5*128,X:STA (sptr),Y:INY
  980LDA CHARS%+6*128,X:STA (sptr),Y:INY
  990LDA CHARS%+7*128,X:STA (sptr),Y
 1000LDX savex:JMP l4
 1010:
 1020.v1
 1030LDA CHARS%-128,X:EOR#&FF:STA (sptr),Y:INY
 1040LDA CHARS%-128+128,X:EOR#&FF:STA (sptr),Y:INY
 1050LDA CHARS%-128+2*128,X:EOR#&FF:STA (sptr),Y:INY
 1060LDA CHARS%-128+3*128,X:EOR#&FF:STA (sptr),Y:INY
 1070LDA CHARS%-128+4*128,X:EOR#&FF:STA (sptr),Y:INY
 1080LDA CHARS%-128+5*128,X:EOR#&FF:STA (sptr),Y:INY
 1090LDA CHARS%-128+6*128,X:EOR#&FF:STA (sptr),Y:INY
 1100LDA CHARS%-128+7*128,X:EOR#&FF:STA (sptr),Y
 1110LDX savex:JMP l4
 1120:
 1130.curson PHP:SEI:LDA#1:STAccount:LSRA:STAcblink:STAcah:TYA:ASLA:ASLA:ASLA:STAcal:ASLA:ROLcah:ASLA:ROLcah:ADCcal:BCCcc1:INCcah:CLC:.cc1
 1140STXcal:ADCcal:BCCcc2:INCcah:.cc2
 1150ASLA:ROLcah:ASLA:ROLcah:ASLA:ROLcah
 1160ADC#FNlo(SA%):STAcal:LDAcah:ADC#FNhi(SA%):STAcah:PLP:RTS
 1170:
 1180.cflip
 1190LDY#7:.clp LDA(cal),Y:EOR#&FF:STA(cal),Y:DEY:BPLclp:RTS
 1200:
 1210.cursoff
 1220LDAccount:BMIcox:PHP:SEI:LSRcblink:BCCco1
 1230JSRcflip:.co1 SEC:RORccount:PLP:.cox RTS
 1240:
 1250:
 1260.keyb PHP:SEI:JSRkey:BEQkb1:JSRkeyin:.kb1 PLP:RTS
 1270:
 1280.key CMP#&20:BCCy1:CMP#&60:BCCy4
 1290BEQy1:CMP#&7F:BCSy20:AND#&5F:BNEy4
 1300:
 1310.y20 BEQy1
 1320AND#&8F:CMP#&82:BCCfkey
 1330.y1 LDX#ktab2-ktab1
 1340.y2 CMPktab1-1,X:BEQy3:DEX:BNEy2:RTS
 1350]
 1360FORP%=P%TO&B10:?P%=&10:NEXT
 1370[OPTopt%
 1380.fkey AND#1:STAgmode:LSRA:RTS
 1390:
 1400.y3 LDAktab2-1,X
 1410:
 1420.y4 TAX:CPX#&40:BCCy5
 1430LDA&25A:AND#&38:EOR#&30:BEQyx:BNEy6
 1440.y5 CPX#&20:BCSyx
 1450LDA&25A:AND#&08:BEQyx
 1460.y6 TXA:EOR#&80:TAX
 1470.yx
 1480CPX#&C0:BCSy9:.y8 TXA:RTS
 1490.y9 LDAgmode:BEQ y8
 1500LDA ktab3-&C0,X:ORA#&80:RTS
 1510:
 1520.ktab1
 1530EQUB &8F
 1540EQUB &8E:EQUB &8D:EQUB &8C
 1550EQUB &8B:EQUB &7F:EQUB &60
 1560EQUB &1B:EQUB &0D:EQUB &09
 1570.ktab2
 1580EQUB &91
 1590EQUB &11:EQUB &1D:EQUB &9D
 1600EQUB &13:EQUB &14:EQUB &5F
 1610EQUB &03:EQUB &0D:EQUB &12
 1620.ktab3
 1630EQUS "3<*57#89/."
 1640EQUS "-=:""!12,$>"
 1650EQUS "%06;4'?+(]&)"
 1660.gmode EQUB 0
 1670:
 1680:
 1690.udtim DECtrg:BPLudx:LDA#4:STAtrg
 1700CLC:LDAtim+2:ADC#6:STAtim+2:BCCud1:INCtim+1:BNEud1:INCtim:.ud1
 1710LDX#0:.ud2 LDAtim,X:CMPtmax,X:BCCudx:INX:CPX#3:BNEud2
 1720LDA#0:.ud3 STAtim-1,X:DEX:BNEud3:.udx RTS
 1730.tmax EQUB&4F:EQUB&1A:EQUB&01:\24*60*60*60+1
 1740.trg EQUB 4
 1750:
 1760:
 1770.break LDYbpnt+1:BMIbr0:DEY:BEQbr0
 1780LDA#FNlo(brmsg-1):STAbpnt:LDA#FNhi(brmsg-1):STAbpnt+1
 1790.br0 LDY#1:.br1 LDA(bpnt),Y:BEQbr2
 1800CMP#&60:BCCbr3:AND#&5F:.br3 JSRwrt:INY:BNEbr1:.br2 JMPtyperr
 1810.brmsg EQUS "BROKEN":EQUB 0
 1820:
 1830.end
 1840]:NEXT:PRINT"BYTES=";end-CODE%:G=GET:ENDPROC
