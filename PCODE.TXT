   10REM P20 05/09/23
   20:
   30*TV255,1
   40:
   50MODE 7
   60:
   70SA%=&60C0
   80SCR2%=SA%-1000
   90:
  100:
  110SCR%=SCR2%-1000
  120CHARS%=SCR%-1024
  130CZ%=128
  140R%=&2D00
  150PROCmk("C")
  160:
  170SCR%=&8400
  180CHARS%=SCR%+1024
  190R%=CHARS%+2048
  200CZ%=256
  210PROCmk("S")
  220END
  230:
  240:
  250DEFPROCmk(d$)
  260PRINT "BASIC @ ";~R%
  270keyin=R%+3
  280typerr=R%+6
  290wrt=R%+9
  300:
  310PROCass(3)
  320OSCLI "SAVE "+d$+".DSPLY 0900 "+STR$~end
  330ENDPROC
  340:
  350:
  360DEFFNw(A%)=!A%AND&FFFF
  370DEFFNlo(A%)=A%AND&FF
  380DEFFNhi(A%)=(A%AND&FF00)DIV256
  390:
  400DEFPROCass(V%)
  410brkv=&202:eventv=&220:insv=&22A
  420bpnt=&FD
  430sbot=SA%:stop=sbot+25*40*8
  440sptr=&FA:pcount=&FD:savex=&F5
  450cal=&F8:cah=cal+1
  460cblink=&F6:ccount=&F7
  470:
  480sndv=&D0:snde=sndv+1
  490sndc=snde+1:sndt=sndc+1
  500sndn=sndt+1
  510:
  520flg=&90
  530tim=&8D
  540:
  550CODE%=&900
  560LOCALP%,opt%:FORopt%=0TOV%STEPV%
  570P%=CODE%:[OPTopt%
  580JMP reset
  590JMP trap
  600JMP curson
  610JMP cursoff
  620JMP pokevia
  630:
  640.trap SEI
  650LDA&F4:STArr1+1:STArr2+1
  660LDA#FNlo(break):STAbrkv:LDA#FNhi(break):STAbrkv+1
  670LDA#FNlo(vsync):STAeventv:LDA#FNhi(vsync):STAeventv+1
  680LDA#FNlo(keyb):STAinsv:LDA#FNhi(keyb):STAinsv+1
  690CLI:JSRinit:JSRreset:SEC:RORccount:LDA#0:STAgmode:JMPsndreset
  700:
  710:
  720.reset LDY#250:.r1
  730LDA SCR2%-1,Y:EOR#&FF:STA SCR%-1,Y
  740LDA SCR2%+250-1,Y:EOR#&FF:STA SCR%+250-1,Y
  750LDA SCR2%+500-1,Y:EOR#&FF:STA SCR%+500-1,Y
  760LDA SCR2%+750-1,Y:EOR#&FF:STA SCR%+750-1,Y
  770DEY:BNE r1:LSRflg:RTS
  780:
  790.init LDA#0:EQUB&2C
  800.clr LDA#32:LDY#250:.c1
  810STA SCR2%-1,Y:STA SCR2%+250-1,Y:STA SCR2%+500-1,Y:STA SCR2%+750-1,Y
  820DEY:BNE c1:RTS
  830:
  840.cpoll DECccount:BNEcpx
  850INCcblink:LDA#17:STAccount:JSRcflip:.cpx JMPvsy
  860:
  870.vsync CMP#4:BNE vsx:LDA&F4:.rr1 CMP#0:BNEvsz
  880PHP:SEI:TXA:PHA:TYA:PHA:JSRudtim:BITflg:BPLscan:BITccount:BPLcpoll
  890.vsy PLA:TAY:PLA:TAX:PLP:.vsz LDA#4:.vsx RTS
  900:
  910.scan
  920LDA#FNhi(SCR2%)+3:STA l1+2
  930LDA#FNhi(SCR%)+3:STA l2+2:STA l3+2
  940LDA#FNlo(stop):STA sptr
  950LDA#FNhi(stop):STA sptr+1
  960LDA#4:STApcount:LDX#FNlo(1000)
  970:
  980.l0 DEX
  990.k3 SEC:LDA sptr:SBC#8:STA sptr
 1000BCC k1:.k2
 1010.l1 LDA SCR2%,X
 1020.l2 CMP SCR%,X:BNE l3
 1030.l4 TXA:BNE l0
 1040DEC pcount:BEQl5
 1050DEC l1+2:DEC l2+2:DEC l3+2
 1060BNE l0
 1070:
 1080.l5 SEC:RORflg:BMIvsy
 1090.k1 DEC sptr+1:BNE k2
 1100:
 1110.l3 STA SCR%,X:STX savex
 1120LDY#0:TAX:BMI v1
 1130:
 1140TXA:.vv1 ORA#0:TAX
 1150LDA CHARS%,X:STA (sptr),Y:INY
 1160LDA CHARS%+CZ%,X:STA (sptr),Y:INY
 1170LDA CHARS%+2*CZ%,X:STA (sptr),Y:INY
 1180LDA CHARS%+3*CZ%,X:STA (sptr),Y:INY
 1190LDA CHARS%+4*CZ%,X:STA (sptr),Y:INY
 1200LDA CHARS%+5*CZ%,X:STA (sptr),Y:INY
 1210LDA CHARS%+6*CZ%,X:STA (sptr),Y:INY
 1220LDA CHARS%+7*CZ%,X:STA (sptr),Y
 1230LDX savex:JMP l4
 1240:
 1250.v1
 1260TXA:AND#&7F:.vv2 ORA#0:TAX
 1270LDA CHARS%,X:EOR#&FF:STA (sptr),Y:INY
 1280LDA CHARS%+CZ%,X:EOR#&FF:STA (sptr),Y:INY
 1290LDA CHARS%+2*CZ%,X:EOR#&FF:STA (sptr),Y:INY
 1300LDA CHARS%+3*CZ%,X:EOR#&FF:STA (sptr),Y:INY
 1310LDA CHARS%+4*CZ%,X:EOR#&FF:STA (sptr),Y:INY
 1320LDA CHARS%+5*CZ%,X:EOR#&FF:STA (sptr),Y:INY
 1330LDA CHARS%+6*CZ%,X:EOR#&FF:STA (sptr),Y:INY
 1340LDA CHARS%+7*CZ%,X:EOR#&FF:STA (sptr),Y
 1350LDX savex:JMP l4
 1360:
 1370.curson PHP:SEI:LDA#1:STAccount:LSRA:STAcblink:STAcah:TYA:ASLA:ASLA:ASLA:STAcal:ASLA:ROLcah:ASLA:ROLcah:ADCcal:BCCcc1:INCcah:CLC:.cc1
 1380STXcal:ADCcal:BCCcc2:INCcah:.cc2
 1390ASLA:ROLcah:ASLA:ROLcah:ASLA:ROLcah
 1400ADC#FNlo(SA%):STAcal:LDAcah:ADC#FNhi(SA%):STAcah:PLP:RTS
 1410:
 1420.cflip
 1430LDY#7:.clp LDA(cal),Y:EOR#&FF:STA(cal),Y:DEY:BPLclp:RTS
 1440:
 1450.cursoff
 1460LDAccount:BMIcox:PHP:SEI:LSRcblink:BCCco1
 1470JSRcflip:.co1 SEC:RORccount:PLP:.cox RTS
 1480:
 1490]:FORP%=P%TO&B10:?P%=&10:NEXT:[OPTopt%
 1500:
 1510.keyb PHP:SEI:JSRkey:BEQkb1:JSRkeyin:.kb1 PLP:RTS
 1520:
 1530.key CMP#&20:BCCy1:CMP#&60:BCCy4
 1540BEQy1:CMP#&7F:BCSy20:AND#&5F:BNEy4
 1550:
 1560.y20 BEQy1
 1570AND#&8F:CMP#&82:BCCfkey
 1580CMP#&86:BEQvoladj
 1590.y1 LDX#ktab2-ktab1
 1600.y2 CMPktab1-1,X:BEQy3:DEX:BNEy2:RTS
 1610.fkey AND#1:STAgmode:LSRA:RTS
 1620.voladj INCsndv:JMPupsnd
 1630:
 1640.y3 LDAktab2-1,X
 1650:
 1660.y4 TAX:CPX#&40:BCCy5
 1670LDA&25A:AND#&38:EOR#&30:BEQyx:BNEy6
 1680.y5 CPX#&20:BCSyx
 1690LDA&25A:AND#&08:BEQyx
 1700.y6 TXA:EOR#&80:TAX
 1710.yx
 1720CPX#&C0:BCSy9:.y8 TXA:RTS
 1730.y9 LDAgmode:BEQ y8
 1740LDA ktab3-&C0,X:ORA#&80:RTS
 1750:
 1760.ktab1
 1770EQUB &8F
 1780EQUB &8E:EQUB &8D:EQUB &8C
 1790EQUB &8B:EQUB &7F:EQUB &60
 1800EQUB &1B:EQUB &0D:EQUB &09
 1810.ktab2
 1820EQUB &91
 1830EQUB &11:EQUB &1D:EQUB &9D
 1840EQUB &13:EQUB &14:EQUB &5F
 1850EQUB &03:EQUB &0D:EQUB &12
 1860.ktab3
 1870EQUS "3<*57#89/."
 1880EQUS "-=:""!12,$>"
 1890EQUS "%06;4'?+(]&)"
 1900.gmode EQUB 0
 1910:
 1920:
 1930.udtim DECtrg:BPLudx:LDA#4:STAtrg
 1940CLC:LDAtim+2:ADC#6:STAtim+2:BCCud1:INCtim+1:BNEud1:INCtim:.ud1
 1950LDX#0:.ud2 LDAtim,X:CMPtmax,X:BCCudx:INX:CPX#3:BNEud2
 1960LDA#0:.ud3 STAtim-1,X:DEX:BNEud3:.udx RTS
 1970.tmax EQUB&4F:EQUB&1A:EQUB&01:\24*60*60*60+1
 1980.trg EQUB 4
 1990:
 2000:
 2010.break
 2020.rr2 LDA#0:STA&F4:STA&FE30:LDYbpnt+1:BMIbr0:DEY:BEQbr0
 2030LDA#FNlo(brmsg-1):STAbpnt:LDA#FNhi(brmsg-1):STAbpnt+1
 2040.br0 LDY#1:.br1 LDA(bpnt),Y:BEQbr2
 2050CMP#&60:BCCbr3:AND#&5F:.br3 JSRwrt:INY:BNEbr1:.br2 JMPtyperr
 2060.brmsg EQUS "BROKEN":EQUB 0
 2070:
 2080.pokepcr
 2090]
 2100IFCZ%=128:[OPTopt%:RTS:]:GOTO2140
 2110[OPTopt%
 2120LDY#0:AND#&0E:CMP#&0E:BEQpp1:LDY#&80:.pp1 TYA:STAvv1+1:STAvv2+1:JMPreset
 2130]
 2140[OPTopt%
 2150:
 2160\ Y=REG-8, A=VALUE
 2170.pokevia CPY#4:BEQpokepcr
 2180JMPpksnd
 2190:
 2200.sdtab1 EQUB&80:EQUB&A0
 2210.sdtab2 EQUB&90:EQUB&B0
 2220:
 2230.sdpk1 \X=CHANNEL, n=VALUE
 2240LDAsndn:AND#&0F:ORAsdtab1,X
 2250JSRsdpk2
 2260LDAsndn:LSRsndn+1:RORA:LSRsndn+1
 2270RORA:LSRsndn+1:RORA:LSRsndn+1:RORA
 2280:
 2290.sdpk2 \ X PRSERVED
 2300SEI:LDY#&FF:STY&FE43:STA&FE4F
 2310INY:STY&FE40
 2320LDY#2:.sdlp1 DEY:BNE sdlp1
 2330LDY#8:STY&FE40
 2340LDY#4:.sdlp2 DEY:BNE sdlp2
 2350CLI:RTS
 2360:
 2370:
 2380.cycs
 2390LDX#0:LDY#8
 2400CMP#&80:BCScy1:EOR#&FF
 2410.cy1 RORA:BCScy2
 2420EOR#&FF:INX
 2430.cy2 DEY:BNEcy1
 2440TXA:LSRA:RORA:RORA:RORA
 2450STAsndc:JMPupsnd
 2460:
 2470.ps1 DEY:BNE ps2:.ps3 RTS
 2480.ps2 DEY:BEQ cycs:DEY:BNE ps3
 2490AND#16:BEQen1:LDA#&FF
 2500.en1 STAsnde:JMPupsnd
 2510.pksnd INY:DEY:BNE ps1:STAsndt
 2520:
 2530.upsnd
 2540LDAsndt:BEQsd1
 2550LDAsndc:ANDsnde:BNEsd2
 2560.sd1 TAX:JSRsd4:INX
 2570.sd4
 2580LDAsdtab2,X:ORA#&0F:JMPsdpk2
 2590:
 2600.sd2 LDX#0:STXsndn+1:LDAsndt
 2610ASLA:STAsndn:ROLsndn+1:JSRsdpk1
 2620:
 2630STXsndn+1:LDAsndt
 2640LSRA:STAsndn:INX:JSRsdpk1
 2650:
 2660LDX#0:LDAsndc:ASLA:JSRsd3
 2670INX:LDAsndc
 2680:
 2690.sd3 BPLsd4
 2700LDAsndv:AND#&0F:ORAsdtab2,X
 2710JMPsdpk2
 2720:
 2730.sndreset \don't reset vol
 2740LDX#2:LDA#0:.sdrlp STAsnde,X:DEX:BPLsdrlp:RTS
 2750:
 2760.end
 2770]:NEXT:PRINT"BYTES=";end-CODE%:G=GET:ENDPROC
